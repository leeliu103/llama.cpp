#include "mmvq.cuh"
#include "quantize.cuh"
#include "unary.cuh"
#include "vecdotq.cuh"

#include <cstdint>

typedef float (*vec_dot_q_cuda_t)(const void * __restrict__ vbq, const block_q8_1 * __restrict__ bq8_1, const int & kbx, const int & iqs);

static constexpr __device__ vec_dot_q_cuda_t get_vec_dot_q_cuda(ggml_type type) {
    switch (type) {
        case GGML_TYPE_Q4_0:    return vec_dot_q4_0_q8_1;
        case GGML_TYPE_Q4_1:    return vec_dot_q4_1_q8_1;
        case GGML_TYPE_Q5_0:    return vec_dot_q5_0_q8_1;
        case GGML_TYPE_Q5_1:    return vec_dot_q5_1_q8_1;
        case GGML_TYPE_Q8_0:    return vec_dot_q8_0_q8_1;
        case GGML_TYPE_MXFP4:   return vec_dot_mxfp4_q8_1;
        case GGML_TYPE_NVFP4:   return vec_dot_nvfp4_q8_1;
        case GGML_TYPE_Q2_K:    return vec_dot_q2_K_q8_1;
        case GGML_TYPE_Q3_K:    return vec_dot_q3_K_q8_1;
        case GGML_TYPE_Q4_K:    return vec_dot_q4_K_q8_1;
        case GGML_TYPE_Q5_K:    return vec_dot_q5_K_q8_1;
        case GGML_TYPE_Q6_K:    return vec_dot_q6_K_q8_1;
        case GGML_TYPE_IQ2_XXS: return vec_dot_iq2_xxs_q8_1;
        case GGML_TYPE_IQ2_XS:  return vec_dot_iq2_xs_q8_1;
        case GGML_TYPE_IQ2_S:   return vec_dot_iq2_s_q8_1;
        case GGML_TYPE_IQ3_XXS: return vec_dot_iq3_xxs_q8_1;
        case GGML_TYPE_IQ1_S:   return vec_dot_iq1_s_q8_1;
        case GGML_TYPE_IQ1_M:   return vec_dot_iq1_m_q8_1;
        case GGML_TYPE_IQ4_NL:  return vec_dot_iq4_nl_q8_1;
        case GGML_TYPE_IQ4_XS:  return vec_dot_iq4_xs_q8_1;
        case GGML_TYPE_IQ3_S:   return vec_dot_iq3_s_q8_1;
        default:                return nullptr;
    }
}

static constexpr __host__ __device__ int get_vdr_mmvq(ggml_type type) {
    switch (type) {
        case GGML_TYPE_Q4_0:    return VDR_Q4_0_Q8_1_MMVQ;
        case GGML_TYPE_Q4_1:    return VDR_Q4_1_Q8_1_MMVQ;
        case GGML_TYPE_Q5_0:    return VDR_Q5_0_Q8_1_MMVQ;
        case GGML_TYPE_Q5_1:    return VDR_Q5_1_Q8_1_MMVQ;
        case GGML_TYPE_Q8_0:    return VDR_Q8_0_Q8_1_MMVQ;
        case GGML_TYPE_MXFP4:   return VDR_MXFP4_Q8_1_MMVQ;
        case GGML_TYPE_NVFP4:   return VDR_NVFP4_Q8_1_MMVQ;
        case GGML_TYPE_Q2_K:    return VDR_Q2_K_Q8_1_MMVQ;
        case GGML_TYPE_Q3_K:    return VDR_Q3_K_Q8_1_MMVQ;
        case GGML_TYPE_Q4_K:    return VDR_Q4_K_Q8_1_MMVQ;
        case GGML_TYPE_Q5_K:    return VDR_Q5_K_Q8_1_MMVQ;
        case GGML_TYPE_Q6_K:    return VDR_Q6_K_Q8_1_MMVQ;
        case GGML_TYPE_IQ2_XXS: return VDR_IQ2_XXS_Q8_1_MMVQ;
        case GGML_TYPE_IQ2_XS:  return VDR_IQ2_XS_Q8_1_MMVQ;
        case GGML_TYPE_IQ2_S:   return VDR_IQ2_S_Q8_1_MMVQ;
        case GGML_TYPE_IQ3_XXS: return VDR_IQ3_XXS_Q8_1_MMVQ;
        case GGML_TYPE_IQ3_S:   return VDR_IQ3_S_Q8_1_MMVQ;
        case GGML_TYPE_IQ4_NL:  return VDR_IQ4_NL_Q8_1_MMVQ;
        case GGML_TYPE_IQ4_XS:  return VDR_IQ4_XS_Q8_1_MMVQ;
        default:                return 1;
    }
}

enum mmvq_parameter_table_id {
    MMVQ_PARAMETERS_GENERIC = 0,
    MMVQ_PARAMETERS_GCN,
    MMVQ_PARAMETERS_RDNA2,
    MMVQ_PARAMETERS_RDNA3_0,
    MMVQ_PARAMETERS_RDNA4
};

static constexpr __device__ mmvq_parameter_table_id get_device_table_id() {
#if defined(RDNA4)
    return MMVQ_PARAMETERS_RDNA4;
#elif defined(RDNA3_0)
    return MMVQ_PARAMETERS_RDNA3_0;
#elif defined(RDNA2) || defined(RDNA3_5)
    return MMVQ_PARAMETERS_RDNA2;
#elif defined(GCN) || defined(CDNA)
    return MMVQ_PARAMETERS_GCN;
#else
    return MMVQ_PARAMETERS_GENERIC;
#endif
}

static __host__ mmvq_parameter_table_id get_device_table_id(int cc) {
    if (GGML_CUDA_CC_IS_RDNA4(cc)) {
        return MMVQ_PARAMETERS_RDNA4;
    }
    if (GGML_CUDA_CC_IS_RDNA3_0(cc)) {
        return MMVQ_PARAMETERS_RDNA3_0;
    }
    if (GGML_CUDA_CC_IS_RDNA2(cc) || GGML_CUDA_CC_IS_RDNA3_5(cc)) {
        return MMVQ_PARAMETERS_RDNA2;
    }
    if (GGML_CUDA_CC_IS_GCN(cc) || GGML_CUDA_CC_IS_CDNA(cc)) {
        return MMVQ_PARAMETERS_GCN;
    }
    return MMVQ_PARAMETERS_GENERIC;
}

// Per-architecture maximum batch size for which MMVQ should be used for MUL_MAT_ID.
// Returns a value <= MMVQ_MAX_BATCH_SIZE. Default is MMVQ_MAX_BATCH_SIZE.
// Check https://github.com/ggml-org/llama.cpp/pull/20905#issuecomment-4145835627 for details

static constexpr __host__ __device__ int get_mmvq_mmid_max_batch_pascal_older(ggml_type type) {
    switch (type) {
        case GGML_TYPE_IQ1_S:   return 6;
        case GGML_TYPE_IQ1_M:   return 6;
        case GGML_TYPE_IQ2_S:   return 4;
        case GGML_TYPE_IQ2_XS:  return 5;
        case GGML_TYPE_IQ2_XXS: return 5;
        case GGML_TYPE_IQ3_S:   return 4;
        case GGML_TYPE_IQ3_XXS: return 4;
        case GGML_TYPE_IQ4_NL:  return 6;
        case GGML_TYPE_IQ4_XS:  return 5;
        case GGML_TYPE_MXFP4:   return 4;
        case GGML_TYPE_Q2_K:    return 4;
        case GGML_TYPE_Q3_K:    return 4;
        case GGML_TYPE_Q4_0:    return 6;
        case GGML_TYPE_Q4_1:    return 6;
        case GGML_TYPE_Q4_K:    return 5;
        case GGML_TYPE_Q5_0:    return 6;
        case GGML_TYPE_Q5_1:    return 6;
        case GGML_TYPE_Q5_K:    return 5;
        case GGML_TYPE_Q6_K:    return 4;
        case GGML_TYPE_Q8_0:    return 4;
        default:                return MMVQ_MAX_BATCH_SIZE;
    }
}

static constexpr __host__ __device__ int get_mmvq_mmid_max_batch_turing_plus(ggml_type type) {
    switch (type) {
        case GGML_TYPE_IQ2_S:   return 7;
        case GGML_TYPE_IQ3_S:   return 6;
        case GGML_TYPE_IQ3_XXS: return 7;
        case GGML_TYPE_MXFP4:   return 7;
        case GGML_TYPE_Q2_K:    return 7;
        case GGML_TYPE_Q3_K:    return 5;
        default:                return MMVQ_MAX_BATCH_SIZE;
    }
}

static constexpr __host__ __device__ int get_mmvq_mmid_max_batch_gcn(ggml_type type) {
    switch (type) {
        case GGML_TYPE_IQ1_S:   return 5;
        case GGML_TYPE_IQ1_M:   return 5;
        case GGML_TYPE_IQ2_S:   return 4;
        case GGML_TYPE_IQ2_XS:  return 4;
        case GGML_TYPE_IQ2_XXS: return 4;
        case GGML_TYPE_IQ3_S:   return 4;
        case GGML_TYPE_IQ3_XXS: return 4;
        case GGML_TYPE_IQ4_NL:  return 6;
        case GGML_TYPE_IQ4_XS:  return 4;
        case GGML_TYPE_Q2_K:    return 4;
        case GGML_TYPE_Q3_K:    return 4;
        case GGML_TYPE_Q4_0:    return 5;
        case GGML_TYPE_Q4_1:    return 5;
        case GGML_TYPE_Q4_K:    return 4;
        case GGML_TYPE_Q5_K:    return 4;
        case GGML_TYPE_Q6_K:    return 4;
        case GGML_TYPE_Q8_0:    return 4;
        default:                return MMVQ_MAX_BATCH_SIZE;
    }
}

static constexpr __host__ __device__ int get_mmvq_mmid_max_batch_cdna(ggml_type type) {
    switch (type) {
        case GGML_TYPE_IQ2_S:   return 5;
        case GGML_TYPE_IQ2_XS:  return 5;
        case GGML_TYPE_IQ2_XXS: return 5;
        case GGML_TYPE_IQ3_S:   return 4;
        case GGML_TYPE_IQ3_XXS: return 5;
        default:                return MMVQ_MAX_BATCH_SIZE;
    }
}

static constexpr __host__ __device__ int get_mmvq_mmid_max_batch_rdna1_rdna2(ggml_type type) {
    switch (type) {
        case GGML_TYPE_IQ2_S:   return 4;
        case GGML_TYPE_IQ2_XS:  return 4;
        case GGML_TYPE_IQ2_XXS: return 4;
        case GGML_TYPE_IQ3_S:   return 4;
        case GGML_TYPE_IQ3_XXS: return 4;
        case GGML_TYPE_Q2_K:    return 7;
        case GGML_TYPE_Q3_K:    return 4;
        case GGML_TYPE_Q4_K:    return 5;
        case GGML_TYPE_Q5_K:    return 6;
        case GGML_TYPE_Q6_K:    return 5;
        default:                return MMVQ_MAX_BATCH_SIZE;
    }
}

static constexpr __host__ __device__ int get_mmvq_mmid_max_batch_rdna3(ggml_type type) {
    switch (type) {
        case GGML_TYPE_IQ1_S:   return 6;
        case GGML_TYPE_IQ1_M:   return 6;
        case GGML_TYPE_IQ2_S:   return 4;
        case GGML_TYPE_IQ2_XS:  return 4;
        case GGML_TYPE_IQ2_XXS: return 4;
        case GGML_TYPE_IQ3_S:   return 4;
        case GGML_TYPE_IQ3_XXS: return 4;
        case GGML_TYPE_IQ4_NL:  return 6;
        case GGML_TYPE_IQ4_XS:  return 6;
        case GGML_TYPE_Q4_K:    return 4;
        case GGML_TYPE_Q5_K:    return 4;
        case GGML_TYPE_Q6_K:    return 4;
        default:                return MMVQ_MAX_BATCH_SIZE;
    }
}

static constexpr __host__ __device__ int get_mmvq_mmid_max_batch_rdna4(ggml_type type) {
    switch (type) {
        case GGML_TYPE_IQ1_S:   return 7;
        case GGML_TYPE_IQ1_M:   return 7;
        case GGML_TYPE_IQ2_S:   return 4;
        case GGML_TYPE_IQ2_XS:  return 4;
        case GGML_TYPE_IQ2_XXS: return 4;
        case GGML_TYPE_IQ3_S:   return 4;
        case GGML_TYPE_IQ3_XXS: return 4;
        case GGML_TYPE_IQ4_NL:  return 7;
        case GGML_TYPE_IQ4_XS:  return 5;
        case GGML_TYPE_MXFP4:   return 5;
        case GGML_TYPE_Q3_K:    return 4;
        case GGML_TYPE_Q4_0:    return 7;
        case GGML_TYPE_Q4_1:    return 7;
        case GGML_TYPE_Q4_K:    return 4;
        case GGML_TYPE_Q5_0:    return 7;
        case GGML_TYPE_Q5_1:    return 7;
        case GGML_TYPE_Q5_K:    return 5;
        case GGML_TYPE_Q6_K:    return 5;
        case GGML_TYPE_Q8_0:    return 7;
        default:                return MMVQ_MAX_BATCH_SIZE;
    }
}

// Host function: returns the max batch size for the current arch+type at runtime.
int get_mmvq_mmid_max_batch(ggml_type type, int cc) {
    // NVIDIA: Volta, Ada Lovelace, and Blackwell always use MMVQ for MUL_MAT_ID.
    if (GGML_CUDA_CC_IS_NVIDIA(cc)) {
        if (cc == GGML_CUDA_CC_VOLTA || cc >= GGML_CUDA_CC_ADA_LOVELACE) {
            return MMVQ_MAX_BATCH_SIZE;
        }
        if (cc >= GGML_CUDA_CC_TURING) {
            return get_mmvq_mmid_max_batch_turing_plus(type);
        }
        return get_mmvq_mmid_max_batch_pascal_older(type);
    }

    // AMD
    if (GGML_CUDA_CC_IS_AMD(cc)) {
        if (GGML_CUDA_CC_IS_RDNA4(cc)) {
            return get_mmvq_mmid_max_batch_rdna4(type);
        }
        if (GGML_CUDA_CC_IS_RDNA3(cc)) {
            return get_mmvq_mmid_max_batch_rdna3(type);
        }
        if (GGML_CUDA_CC_IS_RDNA1(cc) || GGML_CUDA_CC_IS_RDNA2(cc)) {
            return get_mmvq_mmid_max_batch_rdna1_rdna2(type);
        }
        if (GGML_CUDA_CC_IS_CDNA(cc)) {
            return get_mmvq_mmid_max_batch_cdna(type);
        }
        if (GGML_CUDA_CC_IS_GCN(cc)) {
            return get_mmvq_mmid_max_batch_gcn(type);
        }
    }
    return MMVQ_MAX_BATCH_SIZE;
}

// Device constexpr: returns the max batch size for the current arch+type at compile time.
template <ggml_type type>
static constexpr __device__ int get_mmvq_mmid_max_batch_for_device() {
#if defined(RDNA4)
    return get_mmvq_mmid_max_batch_rdna4(type);
#elif defined(RDNA3)
    return get_mmvq_mmid_max_batch_rdna3(type);
#elif defined(RDNA2) || defined(RDNA1)
    return get_mmvq_mmid_max_batch_rdna1_rdna2(type);
#elif defined(CDNA)
    return get_mmvq_mmid_max_batch_cdna(type);
#elif defined(GCN)
    return get_mmvq_mmid_max_batch_gcn(type);
#elif defined(__CUDA_ARCH__) && (__CUDA_ARCH__ == GGML_CUDA_CC_VOLTA || __CUDA_ARCH__ >= GGML_CUDA_CC_ADA_LOVELACE)
    return MMVQ_MAX_BATCH_SIZE;
#elif defined(__CUDA_ARCH__) && __CUDA_ARCH__ >= GGML_CUDA_CC_TURING
    return get_mmvq_mmid_max_batch_turing_plus(type);
#else
    return get_mmvq_mmid_max_batch_pascal_older(type);
#endif
}

static constexpr __host__ __device__ int calc_nwarps(ggml_type type, int ncols_dst, mmvq_parameter_table_id table_id) {
    if (table_id == MMVQ_PARAMETERS_GENERIC) {
        switch (ncols_dst) {
            case 1:
            case 2:
            case 3:
            case 4:
                return 4;
            case 5:
            case 6:
            case 7:
            case 8:
                return 2;
            default:
                return 1;
        }
    } else if (table_id == MMVQ_PARAMETERS_GCN) {
        switch (ncols_dst) {
            case 1:
            case 2:
            case 3:
            case 4:
                return 2;
            case 5:
            case 6:
            case 7:
            case 8:
            default:
                return 1;
        }
    }
    if (table_id == MMVQ_PARAMETERS_RDNA4) {
        // nwarps=8 benefits types with simple vec_dot on RDNA4 (ncols_dst=1).
        // Types with complex vec_dot (Q3_K, IQ2_*, IQ3_*) regress due to register
        // pressure and lookup table contention at higher thread counts.
        if (ncols_dst == 1) {
            switch (type) {
                case GGML_TYPE_Q4_0:
                case GGML_TYPE_Q4_1:
                case GGML_TYPE_Q5_0:
                case GGML_TYPE_Q5_1:
                case GGML_TYPE_Q8_0:
                case GGML_TYPE_Q2_K:
                case GGML_TYPE_Q4_K:
                case GGML_TYPE_Q5_K:
                case GGML_TYPE_Q6_K:
                case GGML_TYPE_IQ4_NL:
                case GGML_TYPE_IQ4_XS:
                    return 8;
                default:
                    return 1;
            }
        }
        return 1;
    }
    if (table_id == MMVQ_PARAMETERS_RDNA3_0) {
        // RDNA3 (W7900): stricter whitelist than RDNA4.
        // Q2_K / Q5_K / IQ4_XS regress in full quant sweeps.
        if (ncols_dst == 1) {
            switch (type) {
                case GGML_TYPE_Q4_0:
                case GGML_TYPE_Q4_1:
                case GGML_TYPE_Q5_0:
                case GGML_TYPE_Q5_1:
                case GGML_TYPE_Q8_0:
                case GGML_TYPE_Q4_K:
                case GGML_TYPE_Q6_K:
                case GGML_TYPE_IQ4_NL:
                    return 8;
                default:
                    return 1;
            }
        }
        return 1;
    }
    return 1;
}

static constexpr __host__ __device__ int calc_rows_per_block(int ncols_dst, int table_id, bool small_k = false, int nwarps = 1) {
    if (table_id == MMVQ_PARAMETERS_GENERIC || table_id == MMVQ_PARAMETERS_GCN) {
        switch (ncols_dst) {
            case 1:
                return small_k ? nwarps : 1;
            case 2:
            case 3:
            case 4:
            case 5:
            case 6:
            case 7:
            case 8:
                return 2;
            default:
                return 1;
        }
    }
    return 1;
}

static int calc_q4_K_q8_1_x4_nwarps(
        const int cc, const int nrows_x, const int ncols_x, const bool has_gate_fusion) {
    if (GGML_CUDA_CC_IS_RDNA2(cc) || GGML_CUDA_CC_IS_RDNA3(cc) || GGML_CUDA_CC_IS_RDNA4(cc)) {
        if (has_gate_fusion) {
            return 1;
        }
        if (nrows_x <= 32768 && ncols_x >= 4096) {
            return 4;
        }
        if (nrows_x <= 32768 && ncols_x >= 1024) {
            return 2;
        }
    }
    return 1;
}

struct q4_k_q8_1_x4_rhs {
    int4   q8_lo;
    int4   q8_hi;
    float2 ds;
};

static __device__ __forceinline__ q4_k_q8_1_x4_rhs load_q4_K_q8_1_x4_rhs(
        const block_q8_1_x4 * __restrict__ yx4,
        const int & kby, const int subblock) {
    const int block_offset = kby + subblock;
    const int block_outer  = block_offset >> 2;
    const int block_inner  = block_offset & 3;
    const block_q8_1_x4 * by = yx4 + block_outer;
    const int4 * q8 = (const int4 *) (by->qs + block_inner * 8);

    q4_k_q8_1_x4_rhs rhs;
    rhs.q8_lo = q8[0];
    rhs.q8_hi = q8[1];
    rhs.ds = __half22float2(by->ds[block_inner]);
    return rhs;
}

struct q4_k_scale_min {
    uint32_t scale;
    uint32_t min;
};

struct q4_k_block_header {
    half2    dm;
    uint32_t scale0;
    uint32_t scale4;
    uint32_t scale8;
};

static_assert(sizeof(q4_k_block_header) == 16, "Unexpected q4_k block header size");

static __device__ __forceinline__ q4_k_block_header load_q4_K_block_header(
        const block_q4_K * __restrict__ bq4_K) {
    return ((const q4_k_block_header *) bq4_K)[0];
}

static __device__ __forceinline__ q4_k_scale_min load_q4_K_scale_min(
        const q4_k_block_header & header,
        const int subblock) {
    const uint32_t sc_lo = header.scale0;
    const uint32_t mb_lo = header.scale4;
    const uint32_t sc_hi = (header.scale8 & 0x0F0F0F0Fu) | ((header.scale0 & 0xC0C0C0C0u) >> 2);
    const uint32_t mb_hi = ((header.scale8 & 0xF0F0F0F0u) >> 4) | ((header.scale4 & 0xC0C0C0C0u) >> 2);

    const int shift = 8 * (subblock & 3);
    const uint32_t sc_word = subblock < 4 ? sc_lo : sc_hi;
    const uint32_t mb_word = subblock < 4 ? mb_lo : mb_hi;

    q4_k_scale_min scale_min;
    scale_min.scale = (sc_word >> shift) & 0x3Fu;
    scale_min.min   = (mb_word >> shift) & 0x3Fu;
    return scale_min;
}

static __device__ __forceinline__ float vec_dot_q4_K_q8_1_x4_with_packed(
        const q4_k_block_header & header,
        const uint4 & packed_q4_lo,
        const uint4 & packed_q4_hi,
        const q4_k_q8_1_x4_rhs & rhs,
        const int subblock) {
    const int qs_shift = (subblock & 1) * 4;

    const int v0 = (packed_q4_lo.x >> qs_shift) & 0x0F0F0F0F;
    const int v1 = (packed_q4_lo.y >> qs_shift) & 0x0F0F0F0F;
    const int v2 = (packed_q4_lo.z >> qs_shift) & 0x0F0F0F0F;
    const int v3 = (packed_q4_lo.w >> qs_shift) & 0x0F0F0F0F;
    const int v4 = (packed_q4_hi.x >> qs_shift) & 0x0F0F0F0F;
    const int v5 = (packed_q4_hi.y >> qs_shift) & 0x0F0F0F0F;
    const int v6 = (packed_q4_hi.z >> qs_shift) & 0x0F0F0F0F;
    const int v7 = (packed_q4_hi.w >> qs_shift) & 0x0F0F0F0F;
    const q4_k_scale_min scale_min = load_q4_K_scale_min(header, subblock);

    int q_sum = 0;
    q_sum = ggml_cuda_dp4a(v0, rhs.q8_lo.x, q_sum);
    q_sum = ggml_cuda_dp4a(v1, rhs.q8_lo.y, q_sum);
    q_sum = ggml_cuda_dp4a(v2, rhs.q8_lo.z, q_sum);
    q_sum = ggml_cuda_dp4a(v3, rhs.q8_lo.w, q_sum);
    q_sum = ggml_cuda_dp4a(v4, rhs.q8_hi.x, q_sum);
    q_sum = ggml_cuda_dp4a(v5, rhs.q8_hi.y, q_sum);
    q_sum = ggml_cuda_dp4a(v6, rhs.q8_hi.z, q_sum);
    q_sum = ggml_cuda_dp4a(v7, rhs.q8_hi.w, q_sum);

    const float2 dm = __half22float2(header.dm);
    return rhs.ds.x * (dm.x * scale_min.scale) * q_sum - (dm.y * scale_min.min) * rhs.ds.y;
}

static __device__ __forceinline__ float vec_dot_q4_K_q8_1_x4(
        const void * __restrict__ vbq,
        const block_q8_1_x4 * __restrict__ yx4,
        const int & kby, const int & kbx, const int subblock_pair) {
    const block_q4_K * bq4_K = (const block_q4_K *) vbq + kbx;

    // One lane handles a 64-value pair of Q4_K subblocks so the packed q4 bytes and
    // block header are reused across both 32-value dots.
    const int subblock0 = 2 * subblock_pair;
    const int subblock1 = subblock0 + 1;
    const int qs_idx = subblock_pair * 8;

    const uint4 packed_q4_lo = ((const uint4 *) ((const uint32_t *) bq4_K->qs + qs_idx + 0))[0];
    const uint4 packed_q4_hi = ((const uint4 *) ((const uint32_t *) bq4_K->qs + qs_idx + 4))[0];
    const q4_k_block_header header = load_q4_K_block_header(bq4_K);

    float sum = 0.0f;
    {
        const q4_k_q8_1_x4_rhs rhs = load_q4_K_q8_1_x4_rhs(yx4, kby, subblock0);
        sum += vec_dot_q4_K_q8_1_x4_with_packed(header, packed_q4_lo, packed_q4_hi, rhs, subblock0);
    }
    {
        const q4_k_q8_1_x4_rhs rhs = load_q4_K_q8_1_x4_rhs(yx4, kby, subblock1);
        sum += vec_dot_q4_K_q8_1_x4_with_packed(header, packed_q4_lo, packed_q4_hi, rhs, subblock1);
    }
    return sum;
}

static __device__ __forceinline__ float2 vec_dot_q4_K_q8_1_x4_x2(
        const void * __restrict__ vbq_x,
        const void * __restrict__ vbq_gate,
        const block_q8_1_x4 * __restrict__ yx4,
        const int & kby, const int & kbx, const int subblock_pair) {
    const block_q4_K * bq4_K_x = (const block_q4_K *) vbq_x + kbx;
    const block_q4_K * bq4_K_gate = (const block_q4_K *) vbq_gate + kbx;

    const int subblock0 = 2 * subblock_pair;
    const int subblock1 = subblock0 + 1;
    const int qs_idx = subblock_pair * 8;

    const uint4 packed_q4_x_lo = ((const uint4 *) ((const uint32_t *) bq4_K_x->qs + qs_idx + 0))[0];
    const uint4 packed_q4_x_hi = ((const uint4 *) ((const uint32_t *) bq4_K_x->qs + qs_idx + 4))[0];
    const uint4 packed_q4_gate_lo = ((const uint4 *) ((const uint32_t *) bq4_K_gate->qs + qs_idx + 0))[0];
    const uint4 packed_q4_gate_hi = ((const uint4 *) ((const uint32_t *) bq4_K_gate->qs + qs_idx + 4))[0];
    const q4_k_block_header header_x = load_q4_K_block_header(bq4_K_x);
    const q4_k_block_header header_gate = load_q4_K_block_header(bq4_K_gate);

    float sum_x = 0.0f;
    float sum_gate = 0.0f;

    {
        const q4_k_q8_1_x4_rhs rhs = load_q4_K_q8_1_x4_rhs(yx4, kby, subblock0);
        sum_x += vec_dot_q4_K_q8_1_x4_with_packed(header_x, packed_q4_x_lo, packed_q4_x_hi, rhs, subblock0);
        sum_gate += vec_dot_q4_K_q8_1_x4_with_packed(header_gate, packed_q4_gate_lo, packed_q4_gate_hi, rhs, subblock0);
    }
    {
        const q4_k_q8_1_x4_rhs rhs = load_q4_K_q8_1_x4_rhs(yx4, kby, subblock1);
        sum_x += vec_dot_q4_K_q8_1_x4_with_packed(header_x, packed_q4_x_lo, packed_q4_x_hi, rhs, subblock1);
        sum_gate += vec_dot_q4_K_q8_1_x4_with_packed(header_gate, packed_q4_gate_lo, packed_q4_gate_hi, rhs, subblock1);
    }

    return make_float2(sum_x, sum_gate);
}

static __device__ __forceinline__ float vec_dot_q4_K_q8_1_x4_with_rhs_pair(
        const block_q4_K * __restrict__ bq4_K,
        const q4_k_q8_1_x4_rhs & rhs0,
        const q4_k_q8_1_x4_rhs & rhs1,
        const int subblock_pair) {
    const int subblock0 = 2 * subblock_pair;
    const int subblock1 = subblock0 + 1;
    const int qs_idx = subblock_pair * 8;

    const uint4 packed_q4_lo = ((const uint4 *) ((const uint32_t *) bq4_K->qs + qs_idx + 0))[0];
    const uint4 packed_q4_hi = ((const uint4 *) ((const uint32_t *) bq4_K->qs + qs_idx + 4))[0];
    const q4_k_block_header header = load_q4_K_block_header(bq4_K);

    return vec_dot_q4_K_q8_1_x4_with_packed(header, packed_q4_lo, packed_q4_hi, rhs0, subblock0) +
           vec_dot_q4_K_q8_1_x4_with_packed(header, packed_q4_lo, packed_q4_hi, rhs1, subblock1);
}

template <bool has_gate_fusion>
static __device__ __forceinline__ void accumulate_q4_K_q8_1_x4_iter(
        const void * __restrict__ vx_row,
        const void * __restrict__ vgate_row,
        const block_q8_1_x4 * __restrict__ y_row,
        const int kby, const int kbx, const int subblock_pair,
        float & tmp, float & tmp_gate) {
    if constexpr (has_gate_fusion) {
        const float2 dots = vec_dot_q4_K_q8_1_x4_x2(vx_row, vgate_row, y_row, kby, kbx, subblock_pair);
        tmp += dots.x;
        tmp_gate += dots.y;
    } else {
        tmp += vec_dot_q4_K_q8_1_x4(vx_row, y_row, kby, kbx, subblock_pair);
        GGML_UNUSED(vgate_row);
        GGML_UNUSED(tmp_gate);
    }
}

template <bool has_fusion, bool has_gate_fusion, bool has_ids, int nwarps>
__launch_bounds__(nwarps*ggml_cuda_get_physical_warp_size(), 1)
static __global__ void mul_mat_vec_q4_K_q8_1_x4(
        const void * __restrict__ vx, const void * __restrict__ vy, const int32_t * __restrict__ ids, const ggml_cuda_mm_fusion_args_device fusion, float * __restrict__ dst,
        const uint32_t ncols_x, const uint3 nchannels_y, const uint32_t stride_row_x, const uint32_t stride_col_y,
        const uint32_t stride_col_dst, const uint3 channel_ratio, const uint32_t stride_channel_x,
        const uint32_t stride_channel_y, const uint32_t stride_channel_dst, const uint3 sample_ratio,
        const uint32_t stride_sample_x, const uint32_t stride_sample_y, const uint32_t stride_sample_dst,
        const uint32_t ids_stride) {

    static_assert(!has_gate_fusion || has_fusion, "gate fusion requires fusion support");

    constexpr int warp_size = ggml_cuda_get_physical_warp_size();
    constexpr int qk = ggml_cuda_type_traits<GGML_TYPE_Q4_K>::qk;
    constexpr int q8_blocks_per_q4_block = qk / QK8_1;
    constexpr int lanes_per_q4_block = 4;
    constexpr int blocks_per_iter = nwarps * warp_size / lanes_per_q4_block;

    static_assert(warp_size % lanes_per_q4_block == 0, "Unexpected Q4_K x4 lane grouping");

    const int tid = warp_size*threadIdx.y + threadIdx.x;
    const int row0 = blockIdx.x;
    const int blocks_per_row_x = ncols_x / qk;
    const uint32_t channel_dst = blockIdx.y;

    uint32_t channel_x;
    uint32_t channel_y;
    uint32_t sample_dst;

    if constexpr (has_ids) {
        channel_x  = ids[channel_dst];
        channel_y  = fastmodulo(channel_dst, nchannels_y);
        sample_dst = 0;
    } else {
        channel_x  = fastdiv(channel_dst, channel_ratio);
        channel_y  = channel_dst;
        sample_dst = blockIdx.z;
    }

    const uint32_t sample_x = fastdiv(sample_dst, sample_ratio);
    const uint32_t sample_y = sample_dst;

    const int kbx_offset = sample_x*stride_sample_x + channel_x*stride_channel_x + row0*stride_row_x;

    const block_q8_1_x4 * y_row = ((const block_q8_1_x4 *) vy) + sample_y*stride_sample_y + channel_y*stride_channel_y;
    const void * vx_row = ((const block_q4_K *) vx) + kbx_offset;
    const void * vgate_row = nullptr;
    if constexpr (has_gate_fusion) {
        vgate_row = ((const block_q4_K *) fusion.gate) + kbx_offset;
    }

    float tmp = 0.0f;
    float tmp_gate = 0.0f;

    const int subblock_pair = tid % lanes_per_q4_block;
    const int kby_step = blocks_per_iter * q8_blocks_per_q4_block;

    int kbx = tid / lanes_per_q4_block;
    int kby = kbx * q8_blocks_per_q4_block;

    for (; kbx + blocks_per_iter < blocks_per_row_x; kbx += 2*blocks_per_iter, kby += 2*kby_step) {
        accumulate_q4_K_q8_1_x4_iter<has_gate_fusion>(vx_row, vgate_row, y_row, kby, kbx, subblock_pair, tmp, tmp_gate);
        accumulate_q4_K_q8_1_x4_iter<has_gate_fusion>(vx_row, vgate_row, y_row, kby + kby_step, kbx + blocks_per_iter, subblock_pair, tmp, tmp_gate);
    }

    for (; kbx < blocks_per_row_x; kbx += blocks_per_iter, kby += kby_step) {
        accumulate_q4_K_q8_1_x4_iter<has_gate_fusion>(vx_row, vgate_row, y_row, kby, kbx, subblock_pair, tmp, tmp_gate);
    }

    __shared__ float tmp_shared[(nwarps - 1 > 0) ? nwarps - 1 : 1][warp_size];
    __shared__ float tmp_shared_gate[(has_gate_fusion && (nwarps - 1 > 0)) ? nwarps - 1 : 1][warp_size];
    if constexpr (!has_gate_fusion) {
        (void) tmp_shared_gate;
    }

    if constexpr (nwarps > 1) {
        if (threadIdx.y > 0) {
            tmp_shared[threadIdx.y - 1][threadIdx.x] = tmp;
            if constexpr (has_gate_fusion) {
                tmp_shared_gate[threadIdx.y - 1][threadIdx.x] = tmp_gate;
            }
        }
        __syncthreads();
        if (threadIdx.y > 0) {
            return;
        }

#pragma unroll
        for (int warp = 0; warp < nwarps - 1; ++warp) {
            tmp += tmp_shared[warp][threadIdx.x];
            if constexpr (has_gate_fusion) {
                tmp_gate += tmp_shared_gate[warp][threadIdx.x];
            }
        }
    }

    tmp = warp_reduce_sum<warp_size>(tmp);
    if constexpr (has_gate_fusion) {
        tmp_gate = warp_reduce_sum<warp_size>(tmp_gate);
    }

    if (threadIdx.y == 0 && threadIdx.x == 0) {
        float result = tmp;

        if constexpr (has_fusion) {
            const uint32_t channel_bias = has_ids ? channel_x : channel_dst;

            if (fusion.x_bias != nullptr) {
                const float * x_bias = (const float *) fusion.x_bias + sample_dst*stride_sample_dst + channel_bias*stride_channel_dst + row0;
                result += x_bias[0];
            }

            if constexpr (has_gate_fusion) {
                float gate_value = tmp_gate;
                if (fusion.gate_bias != nullptr) {
                    const float * gate_bias = (const float *) fusion.gate_bias + sample_dst*stride_sample_dst + channel_bias*stride_channel_dst + row0;
                    gate_value += gate_bias[0];
                }
                switch (fusion.glu_op) {
                    case GGML_GLU_OP_SWIGLU:
                        result *= ggml_cuda_op_silu_single(gate_value);
                        break;
                    case GGML_GLU_OP_GEGLU:
                        result *= ggml_cuda_op_gelu_single(gate_value);
                        break;
                    case GGML_GLU_OP_SWIGLU_OAI:
                        result = ggml_cuda_op_swiglu_oai_single(gate_value, result);
                        break;
                    default:
                        result *= gate_value;
                        break;
                }
            }
        }

        dst[sample_dst*stride_sample_dst + channel_dst*stride_channel_dst + row0] = result;
    }

    if constexpr (!has_fusion) {
        GGML_UNUSED(fusion);
    } else if constexpr (!has_gate_fusion) {
        GGML_UNUSED(fusion.gate);
        GGML_UNUSED(fusion.gate_bias);
        GGML_UNUSED(fusion.glu_op);
    }

    if constexpr (!has_ids) {
        GGML_UNUSED(ids);
        GGML_UNUSED(nchannels_y);
    } else {
        GGML_UNUSED(channel_ratio);
    }
    GGML_UNUSED_VARS(stride_col_y, stride_col_dst, ids_stride);
}

__launch_bounds__(ggml_cuda_get_physical_warp_size(), 1)
static __global__ void mul_mat_vec_q4_K_q8_1_x4_rowpair_gate(
        const void * __restrict__ vx, const void * __restrict__ vy, const ggml_cuda_mm_fusion_args_device fusion, float * __restrict__ dst,
        const uint32_t nrows_x, const uint32_t ncols_x,
        const uint32_t stride_row_x, const uint32_t stride_channel_x,
        const uint32_t stride_channel_y, const uint32_t stride_channel_dst, const uint3 channel_ratio,
        const uint32_t stride_sample_x, const uint32_t stride_sample_y, const uint32_t stride_sample_dst,
        const uint3 sample_ratio) {

    constexpr int warp_size = ggml_cuda_get_physical_warp_size();
    constexpr int qk = ggml_cuda_type_traits<GGML_TYPE_Q4_K>::qk;
    constexpr int q8_blocks_per_q4_block = qk / QK8_1;
    constexpr int lanes_per_q4_block = 4;
    constexpr int blocks_per_iter = warp_size / lanes_per_q4_block;

    static_assert(warp_size % lanes_per_q4_block == 0, "Unexpected Q4_K rowpair lane grouping");

    const int tid = threadIdx.x;
    const uint32_t row0 = 2 * blockIdx.x;
    if (row0 >= nrows_x) {
        return;
    }

    const uint32_t row1 = row0 + 1;
    const bool has_row1 = row1 < nrows_x;
    const int blocks_per_row_x = ncols_x / qk;
    const uint32_t channel_dst = blockIdx.y;
    const uint32_t channel_x = fastdiv(channel_dst, channel_ratio);
    const uint32_t channel_y = channel_dst;
    const uint32_t sample_dst = blockIdx.z;
    const uint32_t sample_x = fastdiv(sample_dst, sample_ratio);
    const uint32_t sample_y = sample_dst;

    const int kbx_offset0 = sample_x*stride_sample_x + channel_x*stride_channel_x + row0*stride_row_x;

    const block_q8_1_x4 * y_row = ((const block_q8_1_x4 *) vy) + sample_y*stride_sample_y + channel_y*stride_channel_y;
    const block_q4_K * vx_row0 = ((const block_q4_K *) vx) + kbx_offset0;
    const block_q4_K * vgate_row0 = ((const block_q4_K *) fusion.gate) + kbx_offset0;
    const block_q4_K * vx_row1 = has_row1 ? vx_row0 + stride_row_x : nullptr;
    const block_q4_K * vgate_row1 = has_row1 ? vgate_row0 + stride_row_x : nullptr;

    float tmp0 = 0.0f;
    float tmp0_gate = 0.0f;
    float tmp1 = 0.0f;
    float tmp1_gate = 0.0f;

    const int subblock_pair = tid % lanes_per_q4_block;
    const int kby_step = blocks_per_iter * q8_blocks_per_q4_block;
    const int subblock0 = 2 * subblock_pair;
    const int subblock1 = subblock0 + 1;

    int kbx = tid / lanes_per_q4_block;
    int kby = kbx * q8_blocks_per_q4_block;

    for (; kbx + blocks_per_iter < blocks_per_row_x; kbx += 2*blocks_per_iter, kby += 2*kby_step) {
        const q4_k_q8_1_x4_rhs rhs0 = load_q4_K_q8_1_x4_rhs(y_row, kby, subblock0);
        const q4_k_q8_1_x4_rhs rhs1 = load_q4_K_q8_1_x4_rhs(y_row, kby, subblock1);
        tmp0 += vec_dot_q4_K_q8_1_x4_with_rhs_pair(vx_row0 + kbx, rhs0, rhs1, subblock_pair);
        tmp0_gate += vec_dot_q4_K_q8_1_x4_with_rhs_pair(vgate_row0 + kbx, rhs0, rhs1, subblock_pair);
        if (has_row1) {
            tmp1 += vec_dot_q4_K_q8_1_x4_with_rhs_pair(vx_row1 + kbx, rhs0, rhs1, subblock_pair);
            tmp1_gate += vec_dot_q4_K_q8_1_x4_with_rhs_pair(vgate_row1 + kbx, rhs0, rhs1, subblock_pair);
        }

        const q4_k_q8_1_x4_rhs rhs2 = load_q4_K_q8_1_x4_rhs(y_row, kby + kby_step, subblock0);
        const q4_k_q8_1_x4_rhs rhs3 = load_q4_K_q8_1_x4_rhs(y_row, kby + kby_step, subblock1);
        tmp0 += vec_dot_q4_K_q8_1_x4_with_rhs_pair(vx_row0 + kbx + blocks_per_iter, rhs2, rhs3, subblock_pair);
        tmp0_gate += vec_dot_q4_K_q8_1_x4_with_rhs_pair(vgate_row0 + kbx + blocks_per_iter, rhs2, rhs3, subblock_pair);
        if (has_row1) {
            tmp1 += vec_dot_q4_K_q8_1_x4_with_rhs_pair(vx_row1 + kbx + blocks_per_iter, rhs2, rhs3, subblock_pair);
            tmp1_gate += vec_dot_q4_K_q8_1_x4_with_rhs_pair(vgate_row1 + kbx + blocks_per_iter, rhs2, rhs3, subblock_pair);
        }
    }

    for (; kbx < blocks_per_row_x; kbx += blocks_per_iter, kby += kby_step) {
        const q4_k_q8_1_x4_rhs rhs0 = load_q4_K_q8_1_x4_rhs(y_row, kby, subblock0);
        const q4_k_q8_1_x4_rhs rhs1 = load_q4_K_q8_1_x4_rhs(y_row, kby, subblock1);
        tmp0 += vec_dot_q4_K_q8_1_x4_with_rhs_pair(vx_row0 + kbx, rhs0, rhs1, subblock_pair);
        tmp0_gate += vec_dot_q4_K_q8_1_x4_with_rhs_pair(vgate_row0 + kbx, rhs0, rhs1, subblock_pair);
        if (has_row1) {
            tmp1 += vec_dot_q4_K_q8_1_x4_with_rhs_pair(vx_row1 + kbx, rhs0, rhs1, subblock_pair);
            tmp1_gate += vec_dot_q4_K_q8_1_x4_with_rhs_pair(vgate_row1 + kbx, rhs0, rhs1, subblock_pair);
        }
    }

    tmp0 = warp_reduce_sum<warp_size>(tmp0);
    tmp0_gate = warp_reduce_sum<warp_size>(tmp0_gate);
    tmp1 = warp_reduce_sum<warp_size>(tmp1);
    tmp1_gate = warp_reduce_sum<warp_size>(tmp1_gate);

    if (threadIdx.x == 0) {
        const uint32_t dst_base = sample_dst*stride_sample_dst + channel_dst*stride_channel_dst;
        const uint32_t bias_base = sample_dst*stride_sample_dst + channel_dst*stride_channel_dst;

        float result0 = tmp0;
        if (fusion.x_bias != nullptr) {
            const float * x_bias = (const float *) fusion.x_bias + bias_base + row0;
            result0 += x_bias[0];
        }
        float gate_value0 = tmp0_gate;
        if (fusion.gate_bias != nullptr) {
            const float * gate_bias = (const float *) fusion.gate_bias + bias_base + row0;
            gate_value0 += gate_bias[0];
        }
        switch (fusion.glu_op) {
            case GGML_GLU_OP_SWIGLU:
                result0 *= ggml_cuda_op_silu_single(gate_value0);
                break;
            case GGML_GLU_OP_GEGLU:
                result0 *= ggml_cuda_op_gelu_single(gate_value0);
                break;
            case GGML_GLU_OP_SWIGLU_OAI:
                result0 = ggml_cuda_op_swiglu_oai_single(gate_value0, result0);
                break;
            default:
                result0 *= gate_value0;
                break;
        }
        dst[dst_base + row0] = result0;

        if (has_row1) {
            float result1 = tmp1;
            if (fusion.x_bias != nullptr) {
                const float * x_bias = (const float *) fusion.x_bias + bias_base + row1;
                result1 += x_bias[0];
            }
            float gate_value1 = tmp1_gate;
            if (fusion.gate_bias != nullptr) {
                const float * gate_bias = (const float *) fusion.gate_bias + bias_base + row1;
                gate_value1 += gate_bias[0];
            }
            switch (fusion.glu_op) {
                case GGML_GLU_OP_SWIGLU:
                    result1 *= ggml_cuda_op_silu_single(gate_value1);
                    break;
                case GGML_GLU_OP_GEGLU:
                    result1 *= ggml_cuda_op_gelu_single(gate_value1);
                    break;
                case GGML_GLU_OP_SWIGLU_OAI:
                    result1 = ggml_cuda_op_swiglu_oai_single(gate_value1, result1);
                    break;
                default:
                    result1 *= gate_value1;
                    break;
            }
            dst[dst_base + row1] = result1;
        }
    }
}

template <bool has_fusion, bool has_gate_fusion, bool has_ids>
static void launch_mul_mat_vec_q4_K_q8_1_x4(
        const void * vx, const void * vy, const int32_t * ids, const ggml_cuda_mm_fusion_args_device fusion, float * dst,
        const uint32_t ncols_x, const uint3 nchannels_y, const uint32_t stride_row_x, const uint32_t stride_col_y,
        const uint32_t stride_col_dst, const uint3 channel_ratio, const uint32_t stride_channel_x,
        const uint32_t stride_channel_y, const uint32_t stride_channel_dst, const uint3 sample_ratio,
        const uint32_t stride_sample_x, const uint32_t stride_sample_y, const uint32_t stride_sample_dst,
        const dim3 & block_nums, const dim3 & generic_block_dims,
        const uint32_t ids_stride, cudaStream_t stream) {
    const int device = ggml_cuda_get_device();
    const int cc = ggml_cuda_info().devices[device].cc;
    const int nwarps = calc_q4_K_q8_1_x4_nwarps(cc, (int) block_nums.x, (int) ncols_x, has_gate_fusion);

    if constexpr (has_fusion && has_gate_fusion && !has_ids) {
        if (nwarps == 1) {
            const dim3 rowpair_block_nums((block_nums.x + 1) / 2, block_nums.y, block_nums.z);
            const dim3 block_dims(generic_block_dims.x, 1, 1);
            mul_mat_vec_q4_K_q8_1_x4_rowpair_gate<<<rowpair_block_nums, block_dims, 0, stream>>>
                (vx, vy, fusion, dst, (uint32_t) block_nums.x, ncols_x, stride_row_x, stride_channel_x,
                 stride_channel_y, stride_channel_dst, channel_ratio,
                 stride_sample_x, stride_sample_y, stride_sample_dst, sample_ratio);
            return;
        }
    }

    if (nwarps == 4) {
        const dim3 block_dims(generic_block_dims.x, 4, 1);
        mul_mat_vec_q4_K_q8_1_x4<has_fusion, has_gate_fusion, has_ids, 4><<<block_nums, block_dims, 0, stream>>>
            (vx, vy, ids, fusion, dst, ncols_x, nchannels_y, stride_row_x, stride_col_y, stride_col_dst,
             channel_ratio, stride_channel_x, stride_channel_y, stride_channel_dst,
             sample_ratio, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride);
        return;
    }

    if (nwarps == 2) {
        const dim3 block_dims(generic_block_dims.x, 2, 1);
        mul_mat_vec_q4_K_q8_1_x4<has_fusion, has_gate_fusion, has_ids, 2><<<block_nums, block_dims, 0, stream>>>
            (vx, vy, ids, fusion, dst, ncols_x, nchannels_y, stride_row_x, stride_col_y, stride_col_dst,
             channel_ratio, stride_channel_x, stride_channel_y, stride_channel_dst,
             sample_ratio, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride);
        return;
    }

    {
        const dim3 block_dims(generic_block_dims.x, 1, 1);
        mul_mat_vec_q4_K_q8_1_x4<has_fusion, has_gate_fusion, has_ids, 1><<<block_nums, block_dims, 0, stream>>>
            (vx, vy, ids, fusion, dst, ncols_x, nchannels_y, stride_row_x, stride_col_y, stride_col_dst,
             channel_ratio, stride_channel_x, stride_channel_y, stride_channel_dst,
             sample_ratio, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride);
    }
}

static void dispatch_mul_mat_vec_q4_K_q8_1_x4(
        const void * vx, const void * vy, const int32_t * ids, const ggml_cuda_mm_fusion_args_device fusion, float * dst,
        const uint32_t ncols_x, const uint3 nchannels_y, const uint32_t stride_row_x, const uint32_t stride_col_y,
        const uint32_t stride_col_dst, const uint3 channel_ratio, const uint32_t stride_channel_x,
        const uint32_t stride_channel_y, const uint32_t stride_channel_dst, const uint3 sample_ratio,
        const uint32_t stride_sample_x, const uint32_t stride_sample_y, const uint32_t stride_sample_dst,
        const dim3 & block_nums, const dim3 & block_dims,
        const uint32_t ids_stride, cudaStream_t stream) {
    const bool has_fusion = fusion.gate != nullptr || fusion.x_bias != nullptr || fusion.gate_bias != nullptr;

    if (ids != nullptr) {
        if (has_fusion) {
            if (fusion.gate != nullptr) {
                launch_mul_mat_vec_q4_K_q8_1_x4<true, true, true>(
                    vx, vy, ids, fusion, dst, ncols_x, nchannels_y, stride_row_x, stride_col_y, stride_col_dst,
                    channel_ratio, stride_channel_x, stride_channel_y, stride_channel_dst,
                    sample_ratio, stride_sample_x, stride_sample_y, stride_sample_dst,
                    block_nums, block_dims, ids_stride, stream);
            } else {
                launch_mul_mat_vec_q4_K_q8_1_x4<true, false, true>(
                    vx, vy, ids, fusion, dst, ncols_x, nchannels_y, stride_row_x, stride_col_y, stride_col_dst,
                    channel_ratio, stride_channel_x, stride_channel_y, stride_channel_dst,
                    sample_ratio, stride_sample_x, stride_sample_y, stride_sample_dst,
                    block_nums, block_dims, ids_stride, stream);
            }
        } else {
            launch_mul_mat_vec_q4_K_q8_1_x4<false, false, true>(
                vx, vy, ids, fusion, dst, ncols_x, nchannels_y, stride_row_x, stride_col_y, stride_col_dst,
                channel_ratio, stride_channel_x, stride_channel_y, stride_channel_dst,
                sample_ratio, stride_sample_x, stride_sample_y, stride_sample_dst,
                block_nums, block_dims, ids_stride, stream);
        }
        return;
    }

    if (has_fusion) {
        if (fusion.gate != nullptr) {
            launch_mul_mat_vec_q4_K_q8_1_x4<true, true, false>(
                vx, vy, ids, fusion, dst, ncols_x, nchannels_y, stride_row_x, stride_col_y, stride_col_dst,
                channel_ratio, stride_channel_x, stride_channel_y, stride_channel_dst,
                sample_ratio, stride_sample_x, stride_sample_y, stride_sample_dst,
                block_nums, block_dims, ids_stride, stream);
        } else {
            launch_mul_mat_vec_q4_K_q8_1_x4<true, false, false>(
                vx, vy, ids, fusion, dst, ncols_x, nchannels_y, stride_row_x, stride_col_y, stride_col_dst,
                channel_ratio, stride_channel_x, stride_channel_y, stride_channel_dst,
                sample_ratio, stride_sample_x, stride_sample_y, stride_sample_dst,
                block_nums, block_dims, ids_stride, stream);
        }
    } else {
        launch_mul_mat_vec_q4_K_q8_1_x4<false, false, false>(
            vx, vy, ids, fusion, dst, ncols_x, nchannels_y, stride_row_x, stride_col_y, stride_col_dst,
            channel_ratio, stride_channel_x, stride_channel_y, stride_channel_dst,
            sample_ratio, stride_sample_x, stride_sample_y, stride_sample_dst,
            block_nums, block_dims, ids_stride, stream);
    }
}

static int calc_q6_K_q8_1_x4_nwarps(
        const int cc, const int nrows_x, const int ncols_x, const bool has_gate_fusion) {
    if (GGML_CUDA_CC_IS_RDNA2(cc) || GGML_CUDA_CC_IS_RDNA3(cc) || GGML_CUDA_CC_IS_RDNA4(cc)) {
        if (!has_gate_fusion && nrows_x <= 2048) {
            return 1;
        }
        if (has_gate_fusion && ncols_x >= 3072) {
            return 2;
        }
        if (nrows_x <= 32768 && ncols_x >= 3072) {
            return 4;
        }
        if (nrows_x <= 32768 && ncols_x >= 1024) {
            return 2;
        }
    }
    return 1;
}

struct q6_k_q8_1_x4_rhs {
    int   u[QR6_K];
    float d8[QR6_K];
};

static __device__ __forceinline__ q6_k_q8_1_x4_rhs load_q6_K_q8_1_x4_rhs(
        const block_q8_1_x4 * __restrict__ yx4, const int & kby, const int & iqs) {
    // Q6_K touches q8_1 blocks in pairs (0,2), (1,3), (4,6), (5,7). With kby stepping in
    // multiples of 8, both loads stay within the same x4-packed block.
    const int bq8_offset = 2 * QR6_K * (iqs / (QI6_K/2)) + (iqs % (QI6_K/2)) / (QI6_K/4);
    const int block_offset = kby + bq8_offset;
    const block_q8_1_x4 * by = yx4 + (block_offset >> 2);
    const int block_inner = block_offset & 3;
    const int qword_idx = iqs % QI8_1;

    q6_k_q8_1_x4_rhs rhs;
    rhs.u[0]  = by->qs[(block_inner + 0) * 8 + qword_idx];
    rhs.u[1]  = by->qs[(block_inner + 2) * 8 + qword_idx];
    rhs.d8[0] = __low2float(by->ds[block_inner + 0]);
    rhs.d8[1] = __low2float(by->ds[block_inner + 2]);
    return rhs;
}

static __device__ __forceinline__ float vec_dot_q6_K_q8_1_x4_with_rhs(
        const block_q6_K * __restrict__ bq6_K, const q6_k_q8_1_x4_rhs & rhs, const int & iqs) {
    const int scale_offset = (QI6_K/4) * (iqs / (QI6_K/2)) + (iqs % (QI6_K/2)) / (QI6_K/8);
    const int vh_shift = 2 * ((iqs % (QI6_K/2)) / (QI6_K/4));
    const int vh_idx = (QI6_K/4) * (iqs / (QI6_K/2)) + iqs % (QI6_K/4);

    const int vl = get_int_b2(bq6_K->ql, iqs);
    const int vh = get_int_b2(bq6_K->qh, vh_idx) >> vh_shift;

    return vec_dot_q6_K_q8_1_impl_mmvq(vl, vh, rhs.u, bq6_K->scales + scale_offset, bq6_K->d, rhs.d8);
}

static __device__ __forceinline__ float vec_dot_q6_K_q8_1_x4(
        const void * __restrict__ vbq, const block_q8_1_x4 * __restrict__ yx4,
        const int & kby, const int & kbx, const int & iqs) {
    const block_q6_K * bq6_K = (const block_q6_K *) vbq + kbx;
    const q6_k_q8_1_x4_rhs rhs = load_q6_K_q8_1_x4_rhs(yx4, kby, iqs);
    return vec_dot_q6_K_q8_1_x4_with_rhs(bq6_K, rhs, iqs);
}

static __device__ __forceinline__ float2 vec_dot_q6_K_q8_1_x4_x2(
        const void * __restrict__ vbq_x, const void * __restrict__ vbq_gate,
        const block_q8_1_x4 * __restrict__ yx4, const int & kby, const int & kbx, const int & iqs) {
    const block_q6_K * bq6_K_x = (const block_q6_K *) vbq_x + kbx;
    const block_q6_K * bq6_K_gate = (const block_q6_K *) vbq_gate + kbx;
    const q6_k_q8_1_x4_rhs rhs = load_q6_K_q8_1_x4_rhs(yx4, kby, iqs);

    return make_float2(
        vec_dot_q6_K_q8_1_x4_with_rhs(bq6_K_x, rhs, iqs),
        vec_dot_q6_K_q8_1_x4_with_rhs(bq6_K_gate, rhs, iqs));
}

template <bool has_gate_fusion>
static __device__ __forceinline__ void accumulate_q6_K_q8_1_x4_iter(
        const void * __restrict__ vx_row, const void * __restrict__ vgate_row,
        const block_q8_1_x4 * __restrict__ y_row, const int kby, const int kbx, const int kqs,
        float & tmp, float & tmp_gate) {
    if constexpr (has_gate_fusion) {
        const float2 dots = vec_dot_q6_K_q8_1_x4_x2(vx_row, vgate_row, y_row, kby, kbx, kqs);
        tmp += dots.x;
        tmp_gate += dots.y;
    } else {
        tmp += vec_dot_q6_K_q8_1_x4(vx_row, y_row, kby, kbx, kqs);
        GGML_UNUSED(vgate_row);
        GGML_UNUSED(tmp_gate);
    }
}

template <bool has_fusion, bool has_gate_fusion, bool has_ids, int nwarps>
__launch_bounds__(nwarps*ggml_cuda_get_physical_warp_size(), 1)
static __global__ void mul_mat_vec_q6_K_q8_1_x4(
        const void * __restrict__ vx, const void * __restrict__ vy, const int32_t * __restrict__ ids, const ggml_cuda_mm_fusion_args_device fusion, float * __restrict__ dst,
        const uint32_t ncols_x, const uint3 nchannels_y, const uint32_t stride_row_x, const uint32_t stride_col_y,
        const uint32_t stride_col_dst, const uint3 channel_ratio, const uint32_t stride_channel_x,
        const uint32_t stride_channel_y, const uint32_t stride_channel_dst, const uint3 sample_ratio,
        const uint32_t stride_sample_x, const uint32_t stride_sample_y, const uint32_t stride_sample_dst,
        const uint32_t ids_stride) {

    static_assert(!has_gate_fusion || has_fusion, "gate fusion requires fusion support");

    constexpr int warp_size = ggml_cuda_get_physical_warp_size();
    constexpr int qk = ggml_cuda_type_traits<GGML_TYPE_Q6_K>::qk;
    constexpr int qi = ggml_cuda_type_traits<GGML_TYPE_Q6_K>::qi;
    constexpr int vdr = VDR_Q6_K_Q8_1_MMVQ;
    constexpr int blocks_per_iter = vdr * nwarps * warp_size / qi;

    const int tid = warp_size*threadIdx.y + threadIdx.x;
    const int row0 = blockIdx.x;
    const int blocks_per_row_x = ncols_x / qk;
    const uint32_t channel_dst = blockIdx.y;

    uint32_t channel_x;
    uint32_t channel_y;
    uint32_t sample_dst;

    if constexpr (has_ids) {
        channel_x  = ids[channel_dst];
        channel_y  = fastmodulo(channel_dst, nchannels_y);
        sample_dst = 0;
    } else {
        channel_x  = fastdiv(channel_dst, channel_ratio);
        channel_y  = channel_dst;
        sample_dst = blockIdx.z;
    }

    const uint32_t sample_x = fastdiv(sample_dst, sample_ratio);
    const uint32_t sample_y = sample_dst;

    const int kbx_offset = sample_x*stride_sample_x + channel_x*stride_channel_x + row0*stride_row_x;

    const block_q8_1_x4 * y_row = ((const block_q8_1_x4 *) vy) + sample_y*stride_sample_y + channel_y*stride_channel_y;
    const void * vx_row = ((const block_q6_K *) vx) + kbx_offset;
    const void * vgate_row = nullptr;
    if constexpr (has_gate_fusion) {
        vgate_row = ((const block_q6_K *) fusion.gate) + kbx_offset;
    }

    float tmp = 0.0f;
    float tmp_gate = 0.0f;

    const int kqs = vdr * (tid % (qi/vdr));
    const int kby_step = blocks_per_iter * (qk/QK8_1);

    int kbx = tid / (qi/vdr);
    int kby = kbx * (qk/QK8_1);

    for (; kbx + blocks_per_iter < blocks_per_row_x; kbx += 2*blocks_per_iter, kby += 2*kby_step) {
        accumulate_q6_K_q8_1_x4_iter<has_gate_fusion>(vx_row, vgate_row, y_row, kby, kbx, kqs, tmp, tmp_gate);
        accumulate_q6_K_q8_1_x4_iter<has_gate_fusion>(vx_row, vgate_row, y_row, kby + kby_step, kbx + blocks_per_iter, kqs, tmp, tmp_gate);
    }

    for (; kbx < blocks_per_row_x; kbx += blocks_per_iter, kby += kby_step) {
        accumulate_q6_K_q8_1_x4_iter<has_gate_fusion>(vx_row, vgate_row, y_row, kby, kbx, kqs, tmp, tmp_gate);
    }

    __shared__ float tmp_shared[(nwarps - 1 > 0) ? nwarps - 1 : 1][warp_size];
    __shared__ float tmp_shared_gate[(has_gate_fusion && (nwarps - 1 > 0)) ? nwarps - 1 : 1][warp_size];
    if constexpr (!has_gate_fusion) {
        (void) tmp_shared_gate;
    }

    if constexpr (nwarps > 1) {
        if (threadIdx.y > 0) {
            tmp_shared[threadIdx.y - 1][threadIdx.x] = tmp;
            if constexpr (has_gate_fusion) {
                tmp_shared_gate[threadIdx.y - 1][threadIdx.x] = tmp_gate;
            }
        }
        __syncthreads();
        if (threadIdx.y > 0) {
            return;
        }

#pragma unroll
        for (int warp = 0; warp < nwarps - 1; ++warp) {
            tmp += tmp_shared[warp][threadIdx.x];
            if constexpr (has_gate_fusion) {
                tmp_gate += tmp_shared_gate[warp][threadIdx.x];
            }
        }
    }

    tmp = warp_reduce_sum<warp_size>(tmp);
    if constexpr (has_gate_fusion) {
        tmp_gate = warp_reduce_sum<warp_size>(tmp_gate);
    }

    if (threadIdx.y == 0 && threadIdx.x == 0) {
        float result = tmp;

        if constexpr (has_fusion) {
            const uint32_t channel_bias = has_ids ? channel_x : channel_dst;

            if (fusion.x_bias != nullptr) {
                const float * x_bias = (const float *) fusion.x_bias + sample_dst*stride_sample_dst + channel_bias*stride_channel_dst + row0;
                result += x_bias[0];
            }

            if constexpr (has_gate_fusion) {
                float gate_value = tmp_gate;
                if (fusion.gate_bias != nullptr) {
                    const float * gate_bias = (const float *) fusion.gate_bias + sample_dst*stride_sample_dst + channel_bias*stride_channel_dst + row0;
                    gate_value += gate_bias[0];
                }
                switch (fusion.glu_op) {
                    case GGML_GLU_OP_SWIGLU:
                        result *= ggml_cuda_op_silu_single(gate_value);
                        break;
                    case GGML_GLU_OP_GEGLU:
                        result *= ggml_cuda_op_gelu_single(gate_value);
                        break;
                    case GGML_GLU_OP_SWIGLU_OAI:
                        result = ggml_cuda_op_swiglu_oai_single(gate_value, result);
                        break;
                    default:
                        result *= gate_value;
                        break;
                }
            }
        }

        dst[sample_dst*stride_sample_dst + channel_dst*stride_channel_dst + row0] = result;
    }

    if constexpr (!has_fusion) {
        GGML_UNUSED(fusion);
    } else if constexpr (!has_gate_fusion) {
        GGML_UNUSED(fusion.gate);
        GGML_UNUSED(fusion.gate_bias);
        GGML_UNUSED(fusion.glu_op);
    }

    if constexpr (!has_ids) {
        GGML_UNUSED(ids);
        GGML_UNUSED(nchannels_y);
    } else {
        GGML_UNUSED(channel_ratio);
    }
    GGML_UNUSED_VARS(stride_col_y, stride_col_dst, ids_stride);
}

template <bool has_fusion, bool has_gate_fusion, bool has_ids>
static void launch_mul_mat_vec_q6_K_q8_1_x4(
        const void * vx, const void * vy, const int32_t * ids, const ggml_cuda_mm_fusion_args_device fusion, float * dst,
        const uint32_t ncols_x, const uint3 nchannels_y, const uint32_t stride_row_x, const uint32_t stride_col_y,
        const uint32_t stride_col_dst, const uint3 channel_ratio, const uint32_t stride_channel_x,
        const uint32_t stride_channel_y, const uint32_t stride_channel_dst, const uint3 sample_ratio,
        const uint32_t stride_sample_x, const uint32_t stride_sample_y, const uint32_t stride_sample_dst,
        const dim3 & block_nums, const dim3 & generic_block_dims,
        const uint32_t ids_stride, cudaStream_t stream) {
    const int device = ggml_cuda_get_device();
    const int cc = ggml_cuda_info().devices[device].cc;
    const int nwarps = calc_q6_K_q8_1_x4_nwarps(cc, (int) block_nums.x, (int) ncols_x, has_gate_fusion);

    if (nwarps == 4) {
        const dim3 block_dims(generic_block_dims.x, 4, 1);
        mul_mat_vec_q6_K_q8_1_x4<has_fusion, has_gate_fusion, has_ids, 4><<<block_nums, block_dims, 0, stream>>>
            (vx, vy, ids, fusion, dst, ncols_x, nchannels_y, stride_row_x, stride_col_y, stride_col_dst,
             channel_ratio, stride_channel_x, stride_channel_y, stride_channel_dst,
             sample_ratio, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride);
        return;
    }

    if (nwarps == 2) {
        const dim3 block_dims(generic_block_dims.x, 2, 1);
        mul_mat_vec_q6_K_q8_1_x4<has_fusion, has_gate_fusion, has_ids, 2><<<block_nums, block_dims, 0, stream>>>
            (vx, vy, ids, fusion, dst, ncols_x, nchannels_y, stride_row_x, stride_col_y, stride_col_dst,
             channel_ratio, stride_channel_x, stride_channel_y, stride_channel_dst,
             sample_ratio, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride);
        return;
    }

    {
        const dim3 block_dims(generic_block_dims.x, 1, 1);
        mul_mat_vec_q6_K_q8_1_x4<has_fusion, has_gate_fusion, has_ids, 1><<<block_nums, block_dims, 0, stream>>>
            (vx, vy, ids, fusion, dst, ncols_x, nchannels_y, stride_row_x, stride_col_y, stride_col_dst,
             channel_ratio, stride_channel_x, stride_channel_y, stride_channel_dst,
             sample_ratio, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride);
    }
}

static void dispatch_mul_mat_vec_q6_K_q8_1_x4(
        const void * vx, const void * vy, const int32_t * ids, const ggml_cuda_mm_fusion_args_device fusion, float * dst,
        const uint32_t ncols_x, const uint3 nchannels_y, const uint32_t stride_row_x, const uint32_t stride_col_y,
        const uint32_t stride_col_dst, const uint3 channel_ratio, const uint32_t stride_channel_x,
        const uint32_t stride_channel_y, const uint32_t stride_channel_dst, const uint3 sample_ratio,
        const uint32_t stride_sample_x, const uint32_t stride_sample_y, const uint32_t stride_sample_dst,
        const dim3 & block_nums, const dim3 & block_dims,
        const uint32_t ids_stride, cudaStream_t stream) {
    const bool has_fusion = fusion.gate != nullptr || fusion.x_bias != nullptr || fusion.gate_bias != nullptr;

    if (ids != nullptr) {
        if (has_fusion) {
            if (fusion.gate != nullptr) {
                launch_mul_mat_vec_q6_K_q8_1_x4<true, true, true>(
                    vx, vy, ids, fusion, dst, ncols_x, nchannels_y, stride_row_x, stride_col_y, stride_col_dst,
                    channel_ratio, stride_channel_x, stride_channel_y, stride_channel_dst,
                    sample_ratio, stride_sample_x, stride_sample_y, stride_sample_dst,
                    block_nums, block_dims, ids_stride, stream);
            } else {
                launch_mul_mat_vec_q6_K_q8_1_x4<true, false, true>(
                    vx, vy, ids, fusion, dst, ncols_x, nchannels_y, stride_row_x, stride_col_y, stride_col_dst,
                    channel_ratio, stride_channel_x, stride_channel_y, stride_channel_dst,
                    sample_ratio, stride_sample_x, stride_sample_y, stride_sample_dst,
                    block_nums, block_dims, ids_stride, stream);
            }
        } else {
            launch_mul_mat_vec_q6_K_q8_1_x4<false, false, true>(
                vx, vy, ids, fusion, dst, ncols_x, nchannels_y, stride_row_x, stride_col_y, stride_col_dst,
                channel_ratio, stride_channel_x, stride_channel_y, stride_channel_dst,
                sample_ratio, stride_sample_x, stride_sample_y, stride_sample_dst,
                block_nums, block_dims, ids_stride, stream);
        }
        return;
    }

    if (has_fusion) {
        if (fusion.gate != nullptr) {
            launch_mul_mat_vec_q6_K_q8_1_x4<true, true, false>(
                vx, vy, ids, fusion, dst, ncols_x, nchannels_y, stride_row_x, stride_col_y, stride_col_dst,
                channel_ratio, stride_channel_x, stride_channel_y, stride_channel_dst,
                sample_ratio, stride_sample_x, stride_sample_y, stride_sample_dst,
                block_nums, block_dims, ids_stride, stream);
        } else {
            launch_mul_mat_vec_q6_K_q8_1_x4<true, false, false>(
                vx, vy, ids, fusion, dst, ncols_x, nchannels_y, stride_row_x, stride_col_y, stride_col_dst,
                channel_ratio, stride_channel_x, stride_channel_y, stride_channel_dst,
                sample_ratio, stride_sample_x, stride_sample_y, stride_sample_dst,
                block_nums, block_dims, ids_stride, stream);
        }
    } else {
        launch_mul_mat_vec_q6_K_q8_1_x4<false, false, false>(
            vx, vy, ids, fusion, dst, ncols_x, nchannels_y, stride_row_x, stride_col_y, stride_col_dst,
            channel_ratio, stride_channel_x, stride_channel_y, stride_channel_dst,
            sample_ratio, stride_sample_x, stride_sample_y, stride_sample_dst,
            block_nums, block_dims, ids_stride, stream);
    }
}

template <ggml_type type, int ncols_dst, bool has_fusion, bool small_k = false>
__launch_bounds__(calc_nwarps(type, ncols_dst, get_device_table_id())*ggml_cuda_get_physical_warp_size(), 1)
static __global__ void mul_mat_vec_q(
        const void * __restrict__ vx, const void * __restrict__ vy, const int32_t * __restrict__ ids, const ggml_cuda_mm_fusion_args_device fusion, float * __restrict__ dst,
        const uint32_t ncols_x, const uint3 nchannels_y, const uint32_t stride_row_x, const uint32_t stride_col_y,
        const uint32_t stride_col_dst, const uint3 channel_ratio, const uint32_t stride_channel_x,
        const uint32_t stride_channel_y, const uint32_t stride_channel_dst, const uint3 sample_ratio,
        const uint32_t stride_sample_x, const uint32_t stride_sample_y, const uint32_t stride_sample_dst,
        const uint32_t ids_stride) {

    constexpr int qk  = ggml_cuda_type_traits<type>::qk;
    constexpr int qi  = ggml_cuda_type_traits<type>::qi;
    constexpr int vdr = get_vdr_mmvq(type);
    constexpr mmvq_parameter_table_id table_id = get_device_table_id();
    constexpr int nwarps = calc_nwarps(type, ncols_dst, table_id);
    constexpr int rows_per_cuda_block = calc_rows_per_block(ncols_dst, table_id, small_k, nwarps);
    constexpr int warp_size = ggml_cuda_get_physical_warp_size();

    constexpr vec_dot_q_cuda_t vec_dot_q_cuda = get_vec_dot_q_cuda(type);

    const     int tid = warp_size*threadIdx.y + threadIdx.x;
    const     int row0 = rows_per_cuda_block*blockIdx.x;
    const     int blocks_per_row_x = ncols_x / qk;
    constexpr int blocks_per_iter = vdr * nwarps*warp_size / qi;

    const uint32_t channel_dst = blockIdx.y;

    uint32_t channel_x;
    uint32_t channel_y;
    uint32_t sample_dst;

    channel_x  = ncols_dst == 1 && ids ? ids[channel_dst]                     : fastdiv(channel_dst, channel_ratio);
    channel_y  = ncols_dst == 1 && ids ? fastmodulo(channel_dst, nchannels_y) : channel_dst;
    sample_dst = blockIdx.z;

    const uint32_t sample_x    = fastdiv(sample_dst, sample_ratio);
    const uint32_t sample_y    = sample_dst;

    bool use_gate = false;
    bool use_bias = false;
    bool use_gate_bias = false;
    const void * vgate = nullptr;
    const float * x_bias = nullptr;
    const float * gate_bias = nullptr;
    ggml_glu_op active_glu;

    if constexpr (has_fusion) {
        use_gate      = fusion.gate      != nullptr;
        use_bias      = fusion.x_bias    != nullptr;
        use_gate_bias = fusion.gate_bias != nullptr && use_gate;
        vgate         = fusion.gate;
        x_bias        = (const float *) fusion.x_bias;
        gate_bias     = (const float *) fusion.gate_bias;
        active_glu    = fusion.glu_op;
    }


    float x_biases[ncols_dst]    = { 0.0f };
    float gate_biases[ncols_dst] = { 0.0f };
    if constexpr (has_fusion) {
        const uint32_t channel_bias = ids ? channel_x : channel_dst;
        if (use_bias) {
            x_bias = x_bias + sample_dst*stride_sample_dst + channel_bias*stride_channel_dst + row0;
            // 1. Hide latency by prefetching bias and gate here
            // 2. load only on threads that won't die after partial sum calculation
            if (threadIdx.x < rows_per_cuda_block && threadIdx.y == 0 &&
                (rows_per_cuda_block == 1 || uint32_t(row0 + threadIdx.x) < stride_col_dst)) {
#pragma unroll
                for (int j = 0; j < ncols_dst; ++j) {
                    x_biases[j] = x_bias[j * stride_col_dst + threadIdx.x];
                }
            }
        }
        if (use_gate_bias) {
            gate_bias = gate_bias + sample_dst*stride_sample_dst + channel_bias*stride_channel_dst + row0;
            if (threadIdx.x < rows_per_cuda_block && threadIdx.y == 0 &&
                (rows_per_cuda_block == 1 || uint32_t(row0 + threadIdx.x) < stride_col_dst)) {
#pragma unroll
                for (int j = 0; j < ncols_dst; ++j) {
                    gate_biases[j] = gate_bias[j * stride_col_dst + threadIdx.x];
                }
            }
        }
    }

    // partial sum for each thread
    float tmp[ncols_dst][rows_per_cuda_block] = {{0.0f}};
    float tmp_gate[ncols_dst][rows_per_cuda_block] = {{0.0f}};

    const block_q8_1 * y = ((const block_q8_1 *) vy) + sample_y*stride_sample_y + channel_y*stride_channel_y;
    const int kbx_offset = sample_x*stride_sample_x + channel_x*stride_channel_x + row0*stride_row_x;

    for (int kbx = tid / (qi/vdr); kbx < blocks_per_row_x; kbx += blocks_per_iter) {
        const int kby = kbx * (qk/QK8_1); // y block index that aligns with kbx

        // x block quant index when casting the quants to int
        const int kqs = vdr * (tid % (qi/vdr));

#pragma unroll
        for (int j = 0; j < ncols_dst; ++j) {
#pragma unroll
            for (int i = 0; i < rows_per_cuda_block; ++i) {
                tmp[j][i] += vec_dot_q_cuda(
                    vx, &y[j*stride_col_y + kby], kbx_offset + i*stride_row_x + kbx, kqs);
                if constexpr (has_fusion) {
                    if (use_gate) {
                        tmp_gate[j][i] += vec_dot_q_cuda(
                            vgate, &y[j*stride_col_y + kby], kbx_offset + i*stride_row_x + kbx, kqs);
                    }
                }
            }
        }
    }

    __shared__ float tmp_shared[nwarps-1 > 0 ? nwarps-1 : 1][ncols_dst][rows_per_cuda_block][warp_size];
    __shared__ float tmp_shared_gate[(has_fusion && (nwarps-1 > 0)) ? nwarps-1 : 1][ncols_dst][rows_per_cuda_block][warp_size];
    if constexpr (!has_fusion) {
        (void) tmp_shared_gate;
    } else if (!use_gate) {
        (void) tmp_shared_gate;
    }

    if (threadIdx.y > 0) {
#pragma unroll
        for (int j = 0; j < ncols_dst; ++j) {
#pragma unroll
            for (int i = 0; i < rows_per_cuda_block; ++i) {
                tmp_shared[threadIdx.y-1][j][i][threadIdx.x] = tmp[j][i];
                if constexpr (has_fusion) {
                    if (use_gate) {
                        tmp_shared_gate[threadIdx.y-1][j][i][threadIdx.x] = tmp_gate[j][i];
                    }
                }
            }
        }
    }
    __syncthreads();
    if (threadIdx.y > 0) {
        return;
    }

    dst += sample_dst*stride_sample_dst + channel_dst*stride_channel_dst + row0;

    // sum up partial sums and write back result
#pragma unroll
    for (int j = 0; j < ncols_dst; ++j) {
#pragma unroll
        for (int i = 0; i < rows_per_cuda_block; ++i) {
#pragma unroll
            for (int l = 0; l < nwarps-1; ++l) {
                tmp[j][i] += tmp_shared[l][j][i][threadIdx.x];
                if constexpr (has_fusion) {
                    if (use_gate) {
                        tmp_gate[j][i] += tmp_shared_gate[l][j][i][threadIdx.x];
                    }
                }
            }
            tmp[j][i] = warp_reduce_sum<warp_size>(tmp[j][i]);
            if constexpr (has_fusion) {
                if (use_gate) {
                    tmp_gate[j][i] = warp_reduce_sum<warp_size>(tmp_gate[j][i]);
                }
            }
        }

        if (threadIdx.x < rows_per_cuda_block && (rows_per_cuda_block == 1 || uint32_t(row0 + threadIdx.x) < stride_col_dst)) {
            float result = tmp[j][threadIdx.x];
            if constexpr (has_fusion) {
                if (use_bias) {
                    result += x_biases[j];
                }
                if (use_gate) {
                    float gate_value = tmp_gate[j][threadIdx.x];
                    if (use_gate_bias) {
                        gate_value += gate_biases[j];
                    }
                    switch (active_glu) {
                        case GGML_GLU_OP_SWIGLU:
                            result *= ggml_cuda_op_silu_single(gate_value);
                            break;
                        case GGML_GLU_OP_GEGLU:
                            result *= ggml_cuda_op_gelu_single(gate_value);
                            break;
                        case GGML_GLU_OP_SWIGLU_OAI: {
                            result = ggml_cuda_op_swiglu_oai_single(gate_value, result);
                            break;
                        }
                        default:
                            result = result * gate_value;
                            break;
                    }
                }
            }
            dst[j*stride_col_dst + threadIdx.x] = result;
        }
    }

    if constexpr (!has_fusion) {
        GGML_UNUSED_VARS(use_gate, use_bias, use_gate_bias, active_glu, gate_bias, x_bias, tmp_gate);
    }
}

// Dedicated MoE multi-token kernel.
// Grid: (ceil(nrows_x / c_rows_per_block), nchannels_dst)
// Block: (warp_size, ncols_dst) - each warp handles one token independently.
// No shared memory reduction needed since each warp works alone.
template <ggml_type type, int c_rows_per_block>
__launch_bounds__(get_mmvq_mmid_max_batch_for_device<type>()*ggml_cuda_get_physical_warp_size(), 1)
static __global__ void mul_mat_vec_q_moe(
        const void * __restrict__ vx, const void * __restrict__ vy, const int32_t * __restrict__ ids,
        float * __restrict__ dst,
        const uint32_t ncols_x, const uint3 nchannels_y, const uint32_t nrows_x,
        const uint32_t stride_row_x, const uint32_t stride_col_y, const uint32_t stride_col_dst,
        const uint32_t stride_channel_x, const uint32_t stride_channel_y, const uint32_t stride_channel_dst,
        const uint32_t ncols_dst, const uint32_t ids_stride) {

    constexpr int qk  = ggml_cuda_type_traits<type>::qk;
    constexpr int qi  = ggml_cuda_type_traits<type>::qi;
    constexpr int vdr = get_vdr_mmvq(type);
    constexpr int warp_size = ggml_cuda_get_physical_warp_size();

    constexpr vec_dot_q_cuda_t vec_dot_q_cuda = get_vec_dot_q_cuda(type);

    const uint32_t token_idx   = threadIdx.y;
    const int      row0        = c_rows_per_block*blockIdx.x;
    const int      blocks_per_row_x = ncols_x / qk;
    constexpr int  blocks_per_iter  = vdr * warp_size / qi;

    const uint32_t channel_dst = blockIdx.y;

    if (token_idx >= ncols_dst) {
        return;
    }

    const uint32_t channel_x = ids[channel_dst + token_idx * ids_stride];
    const uint32_t channel_y = fastmodulo(channel_dst, nchannels_y);

    const block_q8_1 * y = ((const block_q8_1 *) vy) + channel_y*stride_channel_y + token_idx*stride_col_y;
    const int kbx_offset  = channel_x*stride_channel_x + row0*stride_row_x;

    // partial sum for each thread
    float tmp[c_rows_per_block] = {0.0f};

    for (int kbx = threadIdx.x / (qi/vdr); kbx < blocks_per_row_x; kbx += blocks_per_iter) {
        const int kby = kbx * (qk/QK8_1);
        const int kqs = vdr * (threadIdx.x % (qi/vdr));

#pragma unroll
        for (int i = 0; i < c_rows_per_block; ++i) {
            tmp[i] += vec_dot_q_cuda(vx, &y[kby], kbx_offset + i*stride_row_x + kbx, kqs);
        }
    }

    // Warp-level reduction only - no shared memory needed
#pragma unroll
    for (int i = 0; i < c_rows_per_block; ++i) {
        tmp[i] = warp_reduce_sum<warp_size>(tmp[i]);
    }

    // Write results
    if (threadIdx.x < c_rows_per_block && (c_rows_per_block == 1 || uint32_t(row0 + threadIdx.x) < nrows_x)) {
        dst[channel_dst*stride_channel_dst + token_idx*stride_col_dst + row0 + threadIdx.x] = tmp[threadIdx.x];
    }
}

template<ggml_type type>
static std::pair<dim3, dim3> calc_launch_params(
        const int ncols_dst, const int nrows_x, const int nchannels_dst, const int nsamples_or_ntokens,
        const int warp_size, const mmvq_parameter_table_id table_id, const bool small_k = false) {
    const int nwarps = calc_nwarps(type, ncols_dst, table_id);
    const int rpb = calc_rows_per_block(ncols_dst, table_id, small_k, nwarps);
    const int64_t nblocks = (nrows_x + rpb - 1) / rpb;
    const dim3 block_nums(nblocks, nchannels_dst, nsamples_or_ntokens);
    const dim3 block_dims(warp_size, nwarps, 1);
    return {block_nums, block_dims};
}

template<ggml_type type, int c_ncols_dst, bool small_k = false>
static void mul_mat_vec_q_switch_fusion(
        const void * vx, const void * vy, const int32_t * ids, const ggml_cuda_mm_fusion_args_device fusion, float * dst,
        const uint32_t ncols_x, const uint3 nchannels_y, const uint32_t stride_row_x, const uint32_t stride_col_y,
        const uint32_t stride_col_dst, const uint3 channel_ratio, const uint32_t stride_channel_x,
        const uint32_t stride_channel_y, const uint32_t stride_channel_dst, const uint3 sample_ratio,
        const uint32_t stride_sample_x, const uint32_t stride_sample_y, const uint32_t stride_sample_dst,
        const dim3 & block_nums, const dim3 & block_dims, const int nbytes_shared,
        const uint32_t ids_stride, cudaStream_t stream) {

    const bool has_fusion = fusion.gate != nullptr || fusion.x_bias != nullptr || fusion.gate_bias != nullptr;
    if constexpr (c_ncols_dst == 1) {
        if (has_fusion) {
            mul_mat_vec_q<type, c_ncols_dst, true, small_k><<<block_nums, block_dims, nbytes_shared, stream>>>
                (vx, vy, ids, fusion, dst, ncols_x, nchannels_y, stride_row_x, stride_col_y, stride_col_dst,
                 channel_ratio, stride_channel_x, stride_channel_y, stride_channel_dst,
                 sample_ratio, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride);
            return;
        }
    }

    GGML_ASSERT(!has_fusion && "fusion only supported for ncols_dst=1");

    mul_mat_vec_q<type, c_ncols_dst, false, small_k><<<block_nums, block_dims, nbytes_shared, stream>>>
        (vx, vy, ids, fusion, dst, ncols_x, nchannels_y, stride_row_x, stride_col_y, stride_col_dst,
        channel_ratio, stride_channel_x, stride_channel_y, stride_channel_dst,
        sample_ratio, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride);
}

template <ggml_type type>
static void mul_mat_vec_q_moe_launch(
        const void * vx, const void * vy, const int32_t * ids, float * dst,
        const uint32_t ncols_x, const uint3 nchannels_y, const uint32_t nrows_x,
        const uint32_t stride_row_x, const uint32_t stride_col_y, const uint32_t stride_col_dst,
        const uint32_t stride_channel_x, const uint32_t stride_channel_y, const uint32_t stride_channel_dst,
        const uint32_t ncols_dst, const uint32_t ids_stride,
        const int warp_size, const int nchannels_dst, cudaStream_t stream) {

    constexpr int rows_per_block = 2; // 2 gives best perf based on tuning
    const int64_t nblocks_rows = (nrows_x + rows_per_block - 1) / rows_per_block;
    const dim3 block_nums(nblocks_rows, nchannels_dst);
    const dim3 block_dims(warp_size, ncols_dst);

    mul_mat_vec_q_moe<type, rows_per_block><<<block_nums, block_dims, 0, stream>>>(
        vx, vy, ids, dst, ncols_x, nchannels_y, nrows_x,
        stride_row_x, stride_col_y, stride_col_dst,
        stride_channel_x, stride_channel_y, stride_channel_dst,
        ncols_dst, ids_stride);
}

template <ggml_type type>
static void mul_mat_vec_q_switch_ncols_dst(
        const void * vx, const void * vy, const int32_t * ids, const ggml_cuda_mm_fusion_args_device fusion, float * dst,
        const int ncols_x, const int nrows_x, const int ncols_dst,
        const int stride_row_x, const int stride_col_y, const int stride_col_dst,
        const int nchannels_x, const int nchannels_y, const int nchannels_dst,
        const int stride_channel_x, const int stride_channel_y, const int stride_channel_dst,
        const int nsamples_x, const int nsamples_dst, const int stride_sample_x, const int stride_sample_y, const int stride_sample_dst,
        const int ids_stride, cudaStream_t stream) {

    GGML_ASSERT(ncols_x % ggml_blck_size(type) == 0);
    GGML_ASSERT(ncols_dst <= MMVQ_MAX_BATCH_SIZE);

    const uint3 nchannels_y_fd   = ids ? init_fastdiv_values(nchannels_y) : make_uint3(0, 0, 0);
    const uint3 channel_ratio_fd = ids ? make_uint3(0, 0, 0)              : init_fastdiv_values(nchannels_dst / nchannels_x);
    const uint3 sample_ratio_fd  = init_fastdiv_values(nsamples_dst  / nsamples_x);

    const int device = ggml_cuda_get_device();
    const int                     cc        = ggml_cuda_info().devices[device].cc;
    const int warp_size = ggml_cuda_info().devices[device].warp_size;
    const mmvq_parameter_table_id table_id  = get_device_table_id(cc);

    const bool has_fusion = fusion.gate != nullptr || fusion.x_bias != nullptr || fusion.gate_bias != nullptr;
    const bool has_ids = ids != nullptr;

    const auto should_use_small_k = [&](int c_ncols_dst) {
        // When K is small, increase rows_per_block to match nwarps so each warp has more work to do
        // Trigger when the full thread block covers all K blocks in a single loop iteration and few threads remain idle.
        constexpr int qk                    = ggml_cuda_type_traits<type>::qk;
        constexpr int qi                    = ggml_cuda_type_traits<type>::qi;
        constexpr int vdr                   = get_vdr_mmvq(type);
        const int     blocks_per_row_x      = ncols_x / qk;
        const int     blocks_per_iter_1warp = vdr * warp_size / qi;
        const int     nwarps                = calc_nwarps(type, c_ncols_dst, table_id);
        bool          use                   = nwarps > 1 && blocks_per_row_x < nwarps * blocks_per_iter_1warp;

        constexpr std::array<ggml_type, 2> iq_slow_turing = {
            GGML_TYPE_IQ3_XXS,
            GGML_TYPE_IQ3_S,
        };
        constexpr std::array<ggml_type, 8> iq_slow_other = {
            GGML_TYPE_IQ1_S, GGML_TYPE_IQ1_M,   GGML_TYPE_IQ2_XXS, GGML_TYPE_IQ2_XS,
            GGML_TYPE_IQ2_S, GGML_TYPE_IQ3_XXS, GGML_TYPE_IQ3_S,   GGML_TYPE_IQ4_XS,
        };
        constexpr std::array<ggml_type, 3> slow_pascal = {
            GGML_TYPE_IQ3_S,
            GGML_TYPE_Q2_K,
            GGML_TYPE_Q3_K,
        };

        const bool is_nvidia_turing_plus  = GGML_CUDA_CC_IS_NVIDIA(cc) && cc >= GGML_CUDA_CC_TURING;
        const bool is_nvidia_pascal_older = GGML_CUDA_CC_IS_NVIDIA(cc) && cc < GGML_CUDA_CC_VOLTA;

        if (is_nvidia_turing_plus) {
            if (ncols_dst == 1 &&
                    std::find(iq_slow_turing.begin(), iq_slow_turing.end(), type) != iq_slow_turing.end()) {
                use = false;
            }
        } else if ((ncols_dst == 1 && std::find(iq_slow_other.begin(), iq_slow_other.end(), type) != iq_slow_other.end()) ||
                (is_nvidia_pascal_older && std::find(slow_pascal.begin(), slow_pascal.end(), type) != slow_pascal.end()) ||
                GGML_CUDA_CC_IS_RDNA(cc)) {
            use = false;
        }

        return use;
    };

    if (has_ids && ncols_dst > 1) {
        // Multi-token MUL_MAT_ID path - dedicated MoE kernel
        mul_mat_vec_q_moe_launch<type>(
            vx, vy, ids, dst, ncols_x, nchannels_y_fd, nrows_x,
            stride_row_x, stride_col_y, stride_col_dst,
            stride_channel_x, stride_channel_y, stride_channel_dst,
            ncols_dst, ids_stride, warp_size, nchannels_dst, stream);
        return;
    }

    switch (ncols_dst) {
        case 1: {
            constexpr int c_ncols_dst = 1;

            bool use_small_k = should_use_small_k(c_ncols_dst);

            if (use_small_k) {
                std::pair<dim3, dim3> dims = calc_launch_params<type>(c_ncols_dst, nrows_x, nchannels_dst,
                                                                        nsamples_dst, warp_size, table_id, true);
                mul_mat_vec_q_switch_fusion<type, c_ncols_dst, true>(
                    vx, vy, ids, fusion, dst, ncols_x, nchannels_y_fd, stride_row_x, stride_col_y, stride_col_dst,
                    channel_ratio_fd, stride_channel_x, stride_channel_y, stride_channel_dst, sample_ratio_fd,
                    stride_sample_x, stride_sample_y, stride_sample_dst, dims.first, dims.second, 0, ids_stride,
                    stream);
            } else {
                std::pair<dim3, dim3> dims = calc_launch_params<type>(c_ncols_dst, nrows_x, nchannels_dst,
                                                                        nsamples_dst, warp_size, table_id);
                mul_mat_vec_q_switch_fusion<type, c_ncols_dst>(
                    vx, vy, ids, fusion, dst, ncols_x, nchannels_y_fd, stride_row_x, stride_col_y, stride_col_dst,
                    channel_ratio_fd, stride_channel_x, stride_channel_y, stride_channel_dst, sample_ratio_fd,
                    stride_sample_x, stride_sample_y, stride_sample_dst, dims.first, dims.second, 0, ids_stride,
                    stream);
            }
        } break;
        case 2: {
            constexpr int c_ncols_dst = 2;
            std::pair<dim3, dim3> dims = calc_launch_params<type>(c_ncols_dst, nrows_x, nchannels_dst, nsamples_dst, warp_size, table_id);
            mul_mat_vec_q_switch_fusion<type, c_ncols_dst>(vx, vy, ids, fusion, dst, ncols_x, nchannels_y_fd, stride_row_x, stride_col_y, stride_col_dst,
                 channel_ratio_fd, stride_channel_x, stride_channel_y, stride_channel_dst,
                 sample_ratio_fd, stride_sample_x, stride_sample_y, stride_sample_dst,
                 dims.first, dims.second, 0, ids_stride, stream);
        } break;
        case 3: {
            constexpr int c_ncols_dst = 3;
            std::pair<dim3, dim3> dims = calc_launch_params<type>(c_ncols_dst, nrows_x, nchannels_dst, nsamples_dst, warp_size, table_id);
            mul_mat_vec_q_switch_fusion<type, c_ncols_dst>(vx, vy, ids, fusion, dst, ncols_x, nchannels_y_fd, stride_row_x, stride_col_y, stride_col_dst,
                 channel_ratio_fd, stride_channel_x, stride_channel_y, stride_channel_dst,
                 sample_ratio_fd, stride_sample_x, stride_sample_y, stride_sample_dst,
                 dims.first, dims.second, 0, ids_stride, stream);
        } break;
        case 4: {
            constexpr int c_ncols_dst = 4;
            std::pair<dim3, dim3> dims = calc_launch_params<type>(c_ncols_dst, nrows_x, nchannels_dst, nsamples_dst, warp_size, table_id);
            mul_mat_vec_q_switch_fusion<type, c_ncols_dst>(vx, vy, ids, fusion, dst, ncols_x, nchannels_y_fd, stride_row_x, stride_col_y, stride_col_dst,
                 channel_ratio_fd, stride_channel_x, stride_channel_y, stride_channel_dst,
                 sample_ratio_fd, stride_sample_x, stride_sample_y, stride_sample_dst,
                 dims.first, dims.second, 0, ids_stride, stream);
        } break;
        case 5: {
            constexpr int c_ncols_dst = 5;
            std::pair<dim3, dim3> dims = calc_launch_params<type>(c_ncols_dst, nrows_x, nchannels_dst, nsamples_dst, warp_size, table_id);
            mul_mat_vec_q_switch_fusion<type, c_ncols_dst>(vx, vy, ids, fusion, dst, ncols_x, nchannels_y_fd, stride_row_x, stride_col_y, stride_col_dst,
                 channel_ratio_fd, stride_channel_x, stride_channel_y, stride_channel_dst,
                 sample_ratio_fd, stride_sample_x, stride_sample_y, stride_sample_dst,
                 dims.first, dims.second, 0, ids_stride, stream);
        } break;
        case 6: {
            constexpr int c_ncols_dst = 6;
            std::pair<dim3, dim3> dims = calc_launch_params<type>(c_ncols_dst, nrows_x, nchannels_dst, nsamples_dst, warp_size, table_id);
            mul_mat_vec_q_switch_fusion<type, c_ncols_dst>(vx, vy, ids, fusion, dst, ncols_x, nchannels_y_fd, stride_row_x, stride_col_y, stride_col_dst,
                 channel_ratio_fd, stride_channel_x, stride_channel_y, stride_channel_dst,
                 sample_ratio_fd, stride_sample_x, stride_sample_y, stride_sample_dst,
                 dims.first, dims.second, 0, ids_stride, stream);
        } break;
        case 7: {
            constexpr int c_ncols_dst = 7;
            std::pair<dim3, dim3> dims = calc_launch_params<type>(c_ncols_dst, nrows_x, nchannels_dst, nsamples_dst, warp_size, table_id);
            mul_mat_vec_q_switch_fusion<type, c_ncols_dst>(vx, vy, ids, fusion, dst, ncols_x, nchannels_y_fd, stride_row_x, stride_col_y, stride_col_dst,
                 channel_ratio_fd, stride_channel_x, stride_channel_y, stride_channel_dst,
                 sample_ratio_fd, stride_sample_x, stride_sample_y, stride_sample_dst,
                 dims.first, dims.second, 0, ids_stride, stream);
        } break;
        case 8: {
            constexpr int c_ncols_dst = 8;
            std::pair<dim3, dim3> dims = calc_launch_params<type>(c_ncols_dst, nrows_x, nchannels_dst, nsamples_dst, warp_size, table_id);
            mul_mat_vec_q_switch_fusion<type, c_ncols_dst>(vx, vy, ids, fusion, dst, ncols_x, nchannels_y_fd, stride_row_x, stride_col_y, stride_col_dst,
                 channel_ratio_fd, stride_channel_x, stride_channel_y, stride_channel_dst,
                 sample_ratio_fd, stride_sample_x, stride_sample_y, stride_sample_dst,
                 dims.first, dims.second, 0, ids_stride, stream);
        } break;
        default:
            GGML_ABORT("fatal error");
            break;
    }

    GGML_UNUSED(has_fusion);
}
static void mul_mat_vec_q_switch_type(
        const void * vx, const ggml_type type_x, const void * vy, const int32_t * ids, const ggml_cuda_mm_fusion_args_device fusion, float * dst,
        const int ncols_x, const int nrows_x, const int ncols_dst,
        const int stride_row_x, const int stride_col_y, const int stride_col_dst,
        const int nchannels_x, const int nchannels_y, const int nchannels_dst,
        const int stride_channel_x, const int stride_channel_y, const int stride_channel_dst,
        const int nsamples_x, const int nsamples_dst, const int stride_sample_x, const int stride_sample_y, const int stride_sample_dst,
        const int ids_stride, cudaStream_t stream) {
    switch (type_x) {
        case GGML_TYPE_Q4_0:
            mul_mat_vec_q_switch_ncols_dst<GGML_TYPE_Q4_0>
                (vx, vy, ids, fusion, dst, ncols_x, nrows_x, ncols_dst, stride_row_x, stride_col_y, stride_col_dst,
                 nchannels_x, nchannels_y, nchannels_dst, stride_channel_x, stride_channel_y, stride_channel_dst,
                 nsamples_x, nsamples_dst, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride, stream);
            break;
        case GGML_TYPE_Q4_1:
            mul_mat_vec_q_switch_ncols_dst<GGML_TYPE_Q4_1>
                (vx, vy, ids, fusion, dst, ncols_x, nrows_x, ncols_dst, stride_row_x, stride_col_y, stride_col_dst,
                 nchannels_x, nchannels_y, nchannels_dst, stride_channel_x, stride_channel_y, stride_channel_dst,
                 nsamples_x, nsamples_dst, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride, stream);
            break;
        case GGML_TYPE_Q5_0:
            mul_mat_vec_q_switch_ncols_dst<GGML_TYPE_Q5_0>
                (vx, vy, ids, fusion, dst, ncols_x, nrows_x, ncols_dst, stride_row_x, stride_col_y, stride_col_dst,
                 nchannels_x, nchannels_y, nchannels_dst, stride_channel_x, stride_channel_y, stride_channel_dst,
                 nsamples_x, nsamples_dst, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride, stream);
            break;
        case GGML_TYPE_Q5_1:
            mul_mat_vec_q_switch_ncols_dst<GGML_TYPE_Q5_1>
                (vx, vy, ids, fusion, dst, ncols_x, nrows_x, ncols_dst, stride_row_x, stride_col_y, stride_col_dst,
                 nchannels_x, nchannels_y, nchannels_dst, stride_channel_x, stride_channel_y, stride_channel_dst,
                 nsamples_x, nsamples_dst, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride, stream);
            break;
        case GGML_TYPE_Q8_0:
            mul_mat_vec_q_switch_ncols_dst<GGML_TYPE_Q8_0>
                (vx, vy, ids, fusion, dst, ncols_x, nrows_x, ncols_dst, stride_row_x, stride_col_y, stride_col_dst,
                 nchannels_x, nchannels_y, nchannels_dst, stride_channel_x, stride_channel_y, stride_channel_dst,
                 nsamples_x, nsamples_dst, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride, stream);
            break;
        case GGML_TYPE_MXFP4:
            mul_mat_vec_q_switch_ncols_dst<GGML_TYPE_MXFP4>
                (vx, vy, ids, fusion, dst, ncols_x, nrows_x, ncols_dst, stride_row_x, stride_col_y, stride_col_dst,
                 nchannels_x, nchannels_y, nchannels_dst, stride_channel_x, stride_channel_y, stride_channel_dst,
                 nsamples_x, nsamples_dst, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride, stream);
            break;
        case GGML_TYPE_NVFP4:
            mul_mat_vec_q_switch_ncols_dst<GGML_TYPE_NVFP4>
                (vx, vy, ids, fusion, dst, ncols_x, nrows_x, ncols_dst, stride_row_x, stride_col_y, stride_col_dst,
                 nchannels_x, nchannels_y, nchannels_dst, stride_channel_x, stride_channel_y, stride_channel_dst,
                 nsamples_x, nsamples_dst, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride, stream);
            break;
        case GGML_TYPE_Q2_K:
            mul_mat_vec_q_switch_ncols_dst<GGML_TYPE_Q2_K>
                (vx, vy, ids, fusion, dst, ncols_x, nrows_x, ncols_dst, stride_row_x, stride_col_y, stride_col_dst,
                 nchannels_x, nchannels_y, nchannels_dst, stride_channel_x, stride_channel_y, stride_channel_dst,
                 nsamples_x, nsamples_dst, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride, stream);
            break;
        case GGML_TYPE_Q3_K:
            mul_mat_vec_q_switch_ncols_dst<GGML_TYPE_Q3_K>
                (vx, vy, ids, fusion, dst, ncols_x, nrows_x, ncols_dst, stride_row_x, stride_col_y, stride_col_dst,
                 nchannels_x, nchannels_y, nchannels_dst, stride_channel_x, stride_channel_y, stride_channel_dst,
                 nsamples_x, nsamples_dst, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride, stream);
            break;
        case GGML_TYPE_Q4_K:
            mul_mat_vec_q_switch_ncols_dst<GGML_TYPE_Q4_K>
                (vx, vy, ids, fusion, dst, ncols_x, nrows_x, ncols_dst, stride_row_x, stride_col_y, stride_col_dst,
                 nchannels_x, nchannels_y, nchannels_dst, stride_channel_x, stride_channel_y, stride_channel_dst,
                 nsamples_x, nsamples_dst, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride, stream);
            break;
        case GGML_TYPE_Q5_K:
            mul_mat_vec_q_switch_ncols_dst<GGML_TYPE_Q5_K>
                (vx, vy, ids, fusion, dst, ncols_x, nrows_x, ncols_dst, stride_row_x, stride_col_y, stride_col_dst,
                 nchannels_x, nchannels_y, nchannels_dst, stride_channel_x, stride_channel_y, stride_channel_dst,
                 nsamples_x, nsamples_dst, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride, stream);
            break;
        case GGML_TYPE_Q6_K:
            mul_mat_vec_q_switch_ncols_dst<GGML_TYPE_Q6_K>
                (vx, vy, ids, fusion, dst, ncols_x, nrows_x, ncols_dst, stride_row_x, stride_col_y, stride_col_dst,
                 nchannels_x, nchannels_y, nchannels_dst, stride_channel_x, stride_channel_y, stride_channel_dst,
                 nsamples_x, nsamples_dst, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride, stream);
            break;
        case GGML_TYPE_IQ2_XXS:
            mul_mat_vec_q_switch_ncols_dst<GGML_TYPE_IQ2_XXS>
                (vx, vy, ids, fusion, dst, ncols_x, nrows_x, ncols_dst, stride_row_x, stride_col_y, stride_col_dst,
                 nchannels_x, nchannels_y, nchannels_dst, stride_channel_x, stride_channel_y, stride_channel_dst,
                 nsamples_x, nsamples_dst, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride, stream);
            break;
        case GGML_TYPE_IQ2_XS:
            mul_mat_vec_q_switch_ncols_dst<GGML_TYPE_IQ2_XS>
                (vx, vy, ids, fusion, dst, ncols_x, nrows_x, ncols_dst, stride_row_x, stride_col_y, stride_col_dst,
                 nchannels_x, nchannels_y, nchannels_dst, stride_channel_x, stride_channel_y, stride_channel_dst,
                 nsamples_x, nsamples_dst, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride, stream);
            break;
        case GGML_TYPE_IQ2_S:
            mul_mat_vec_q_switch_ncols_dst<GGML_TYPE_IQ2_S>
                (vx, vy, ids, fusion, dst, ncols_x, nrows_x, ncols_dst, stride_row_x, stride_col_y, stride_col_dst,
                 nchannels_x, nchannels_y, nchannels_dst, stride_channel_x, stride_channel_y, stride_channel_dst,
                 nsamples_x, nsamples_dst, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride, stream);
            break;
        case GGML_TYPE_IQ3_XXS:
            mul_mat_vec_q_switch_ncols_dst<GGML_TYPE_IQ3_XXS>
                (vx, vy, ids, fusion, dst, ncols_x, nrows_x, ncols_dst, stride_row_x, stride_col_y, stride_col_dst,
                 nchannels_x, nchannels_y, nchannels_dst, stride_channel_x, stride_channel_y, stride_channel_dst,
                 nsamples_x, nsamples_dst, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride, stream);
            break;
        case GGML_TYPE_IQ1_S:
            mul_mat_vec_q_switch_ncols_dst<GGML_TYPE_IQ1_S>
                (vx, vy, ids, fusion, dst, ncols_x, nrows_x, ncols_dst, stride_row_x, stride_col_y, stride_col_dst,
                 nchannels_x, nchannels_y, nchannels_dst, stride_channel_x, stride_channel_y, stride_channel_dst,
                 nsamples_x, nsamples_dst, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride, stream);
            break;
        case GGML_TYPE_IQ1_M:
            mul_mat_vec_q_switch_ncols_dst<GGML_TYPE_IQ1_M>
                (vx, vy, ids, fusion, dst, ncols_x, nrows_x, ncols_dst, stride_row_x, stride_col_y, stride_col_dst,
                 nchannels_x, nchannels_y, nchannels_dst, stride_channel_x, stride_channel_y, stride_channel_dst,
                 nsamples_x, nsamples_dst, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride, stream);
            break;
        case GGML_TYPE_IQ4_NL:
            mul_mat_vec_q_switch_ncols_dst<GGML_TYPE_IQ4_NL>
                (vx, vy, ids, fusion, dst, ncols_x, nrows_x, ncols_dst, stride_row_x, stride_col_y, stride_col_dst,
                 nchannels_x, nchannels_y, nchannels_dst, stride_channel_x, stride_channel_y, stride_channel_dst,
                 nsamples_x, nsamples_dst, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride, stream);
            break;
        case GGML_TYPE_IQ4_XS:
            mul_mat_vec_q_switch_ncols_dst<GGML_TYPE_IQ4_XS>
                (vx, vy, ids, fusion, dst, ncols_x, nrows_x, ncols_dst, stride_row_x, stride_col_y, stride_col_dst,
                 nchannels_x, nchannels_y, nchannels_dst, stride_channel_x, stride_channel_y, stride_channel_dst,
                 nsamples_x, nsamples_dst, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride, stream);
            break;
        case GGML_TYPE_IQ3_S:
            mul_mat_vec_q_switch_ncols_dst<GGML_TYPE_IQ3_S>
                (vx, vy, ids, fusion, dst, ncols_x, nrows_x, ncols_dst, stride_row_x, stride_col_y, stride_col_dst,
                 nchannels_x, nchannels_y, nchannels_dst, stride_channel_x, stride_channel_y, stride_channel_dst,
                 nsamples_x, nsamples_dst, stride_sample_x, stride_sample_y, stride_sample_dst, ids_stride, stream);
            break;
        default:
            GGML_ABORT("fatal error");
            break;
    }
}

void ggml_cuda_mul_mat_vec_q(
        ggml_backend_cuda_context & ctx, const ggml_tensor * src0, const ggml_tensor * src1, const ggml_tensor * ids, ggml_tensor * dst,
        const ggml_cuda_mm_fusion_args_host * fusion) {
    GGML_ASSERT(        src1->type == GGML_TYPE_F32);
    GGML_ASSERT(        dst->type  == GGML_TYPE_F32);
    GGML_ASSERT(!ids || ids->type  == GGML_TYPE_I32); // Optional, used for batched GGML_MUL_MAT_ID.

    GGML_TENSOR_BINARY_OP_LOCALS;

    cudaStream_t stream = ctx.stream();

    const size_t ts_src0 = ggml_type_size(src0->type);
    const size_t ts_src1 = ggml_type_size(src1->type);
    const size_t ts_dst  = ggml_type_size(dst->type);

    GGML_ASSERT(        nb00       == ts_src0);
    GGML_ASSERT(        nb10       == ts_src1);
    GGML_ASSERT(        nb0        == ts_dst);
    GGML_ASSERT(!ids || ids->nb[0] == ggml_type_size(ids->type));

    GGML_ASSERT(!ids || ne12 <= MMVQ_MAX_BATCH_SIZE);

    const float   * src1_d =       (const float   *) src1->data;
    const int32_t *  ids_d = ids ? (const int32_t *)  ids->data : nullptr;
    float         *  dst_d =       (float         *)  dst->data;

    ggml_cuda_mm_fusion_args_device fusion_local{};

    if (fusion) {
        GGML_ASSERT( !ids || dst->ne[2] == 1);
        GGML_ASSERT(  ids || dst->ne[1] == 1);

        if (fusion->x_bias) {
            GGML_ASSERT(fusion->x_bias->type == GGML_TYPE_F32);
            GGML_ASSERT(fusion->x_bias->ne[0] == dst->ne[0]);
            GGML_ASSERT(!ids || fusion->x_bias->ne[1] == src0->ne[2]);
            fusion_local.x_bias = fusion->x_bias->data;
        }
        if (fusion->gate) {
            GGML_ASSERT(fusion->gate->type == src0->type && ggml_are_same_stride(fusion->gate, src0));
            fusion_local.gate = fusion->gate->data;
        }
        if (fusion->gate_bias) {
            GGML_ASSERT(fusion->gate_bias->type == GGML_TYPE_F32);
            GGML_ASSERT(fusion->gate_bias->ne[0] == dst->ne[0]);
            GGML_ASSERT(!ids || fusion->gate_bias->ne[1] == src0->ne[2]);
            fusion_local.gate_bias = fusion->gate_bias->data;
        }
        fusion_local.glu_op = fusion->glu_op;
    }

    // If src0 is a temporary compute buffer, clear any potential padding.
    if (ggml_backend_buffer_get_usage(src0->buffer) == GGML_BACKEND_BUFFER_USAGE_COMPUTE) {
        const size_t size_data  = ggml_nbytes(src0);
        const size_t size_alloc = ggml_backend_buffer_get_alloc_size(src0->buffer, src0);
        if (size_alloc > size_data) {
            GGML_ASSERT(ggml_is_contiguously_allocated(src0));
            GGML_ASSERT(!src0->view_src);
            CUDA_CHECK(cudaMemsetAsync((char *) src0->data + size_data, 0, size_alloc - size_data, stream));
        }
    }

    const int64_t ne10_padded = GGML_PAD(ne10, MATRIX_ROW_PADDING);

    // For MUL_MAT_ID the memory layout is different than for MUL_MAT:
    const int64_t ncols_dst     = ids ? ne2  : ne1;
    const int64_t nchannels_y   = ids ? ne11 : ne12;
    const int64_t nchannels_dst = ids ? ne1  : ne2;
    const bool use_q4_K_q8_1_x4 = src0->type == GGML_TYPE_Q4_K && ncols_dst == 1;
    const bool use_q6_K_q8_1_x4 = src0->type == GGML_TYPE_Q6_K && ncols_dst == 1;
    const bool use_q8_1_x4 = use_q4_K_q8_1_x4 || use_q6_K_q8_1_x4;
    const size_t nbytes_src1_q8_1 = ne13*ne12 * ne11*ne10_padded * sizeof(block_q8_1)/QK8_1;
    ggml_cuda_pool_alloc<char> src1_q8_1(ctx.pool(), nbytes_src1_q8_1);
    {
        const int64_t s11 = src1->nb[1] / ts_src1;
        const int64_t s12 = src1->nb[2] / ts_src1;
        const int64_t s13 = src1->nb[3] / ts_src1;
        if (use_q8_1_x4) {
            quantize_row_q8_1_x4_cuda(src1_d, nullptr, src1_q8_1.get(), src0->type, ne10, s11, s12, s13, ne10_padded, ne11, ne12, ne13, stream);
        } else {
            quantize_row_q8_1_cuda(src1_d, nullptr, src1_q8_1.get(), src0->type, ne10, s11, s12, s13, ne10_padded, ne11, ne12, ne13, stream);
        }
    }

    const void * src1_q8_1_d = src1_q8_1.get();

    const int64_t s01 = src0->nb[1] / ts_src0;
    const int64_t s11 = use_q8_1_x4 ? ne10_padded / (4 * QK8_1) : ne10_padded / QK8_1;
    const int64_t s1  =  dst->nb[1] / ts_dst;
    const int64_t s02 = src0->nb[2] / ts_src0;
    const int64_t s2  =  dst->nb[2] / ts_dst;
    const int64_t s03 = src0->nb[3] / ts_src0;
    const int64_t s3  =  dst->nb[3] / ts_dst;

    const int64_t s12 = ne11*s11;
    const int64_t s13 = ne12*s12;

    const int64_t stride_col_dst     = ids ? s2   : s1;
    const int64_t stride_col_y       = ids ? s12  : s11;
    const int64_t stride_channel_dst = ids ? s1   : s2;
    const int64_t stride_channel_y   = ids ? s11  : s12;

    const int64_t ids_stride = ids ? ids->nb[1] / ggml_type_size(ids->type) : 0;

    if (use_q8_1_x4) {
        const uint3 nchannels_y_fd   = ids ? init_fastdiv_values((int) nchannels_y) : make_uint3(0, 0, 0);
        const uint3 channel_ratio_fd = ids ? make_uint3(0, 0, 0) : init_fastdiv_values((int) (nchannels_dst / ne02));
        const uint3 sample_ratio_fd  = init_fastdiv_values((int) (ne3 / ne03));

        const int device = ggml_cuda_get_device();
        const int warp_size = ggml_cuda_info().devices[device].warp_size;
        const mmvq_parameter_table_id table_id = get_device_table_id(ggml_cuda_info().devices[device].cc);

        if (use_q4_K_q8_1_x4) {
            const std::pair<dim3, dim3> dims = calc_launch_params<GGML_TYPE_Q4_K>(
                1, (int) ne01, (int) nchannels_dst, (int) ne3, warp_size, table_id);
            dispatch_mul_mat_vec_q4_K_q8_1_x4(
                src0->data, src1_q8_1_d, ids_d, fusion_local, dst_d, (uint32_t) ne00,
                nchannels_y_fd, (uint32_t) s01, (uint32_t) stride_col_y, (uint32_t) stride_col_dst,
                channel_ratio_fd, (uint32_t) s02, (uint32_t) stride_channel_y, (uint32_t) stride_channel_dst,
                sample_ratio_fd, (uint32_t) s03, (uint32_t) s13, (uint32_t) s3,
                dims.first, dims.second, (uint32_t) ids_stride, stream);
            return;
        }

        const std::pair<dim3, dim3> dims = calc_launch_params<GGML_TYPE_Q6_K>(
            1, (int) ne01, (int) nchannels_dst, (int) ne3, warp_size, table_id);
        dispatch_mul_mat_vec_q6_K_q8_1_x4(
            src0->data, src1_q8_1_d, ids_d, fusion_local, dst_d, (uint32_t) ne00,
            nchannels_y_fd, (uint32_t) s01, (uint32_t) stride_col_y, (uint32_t) stride_col_dst,
            channel_ratio_fd, (uint32_t) s02, (uint32_t) stride_channel_y, (uint32_t) stride_channel_dst,
            sample_ratio_fd, (uint32_t) s03, (uint32_t) s13, (uint32_t) s3,
            dims.first, dims.second, (uint32_t) ids_stride, stream);
        return;
    }

    mul_mat_vec_q_switch_type(
        src0->data, src0->type, src1_q8_1_d, ids_d, fusion_local, dst_d, ne00,
        ne01,              ncols_dst,     s01, stride_col_y,     stride_col_dst,
        ne02, nchannels_y, nchannels_dst, s02, stride_channel_y, stride_channel_dst,
        ne03,              ne3,           s03, s13,              s3,               ids_stride, stream);
}

void ggml_cuda_op_mul_mat_vec_q(
    ggml_backend_cuda_context & ctx,
    const ggml_tensor * src0, const ggml_tensor * src1, ggml_tensor * dst, const char * src0_dd_i, const float * src1_ddf_i,
    const char * src1_ddq_i, float * dst_dd_i, const int64_t row_low, const int64_t row_high, const int64_t src1_ncols,
    const int64_t src1_padded_row_size, cudaStream_t stream) {

    const int64_t ne00 = src0->ne[0];
    const int64_t row_diff = row_high - row_low;

    const int64_t ne10 = src1->ne[0];
    GGML_ASSERT(ne10 % QK8_1 == 0);

    const int64_t ne0 = dst->ne[0];

    int id = ggml_cuda_get_device();

    // the main device has a larger memory buffer to hold the results from all GPUs
    // nrows_dst == nrows of the matrix that the kernel writes into
    const int64_t nrows_dst = id == ctx.device ? ne0 : row_diff;

    const int stride_row_x = ne00 / ggml_blck_size(src0->type);
    const int stride_col_y = src1_padded_row_size / QK8_1;

    ggml_cuda_mm_fusion_args_device fusion_local{};
    mul_mat_vec_q_switch_type(
        src0_dd_i, src0->type, src1_ddq_i, nullptr, fusion_local, dst_dd_i, ne00, row_diff, src1_ncols, stride_row_x, stride_col_y, nrows_dst,
        1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 0, stream);

    GGML_UNUSED_VARS(src1, dst, src1_ddf_i, src1_ncols, src1_padded_row_size);
}
